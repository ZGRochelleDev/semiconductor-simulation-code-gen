     DEMO CIRCUIT2. INPUT FILE
*
.SUBCKT  SUB1  1 2  3 
Q2  1 25 3  QNL
RS2  25 2  0.1K
.ENDS
.SUBCKT  SUB2  1 2 
Q3  1 2 VEE  QNL
Q4  2 2 VEE  QNL  1
.ENDS
.GLOBAL  VEE 
*......... PART I. COMPONENTS and CIRCUITS ........................
VIN  1 0  DC -0.25  PULSE( -0.25 0.25 1N 2N 2N 18N 40N )  AC 1
VEE  VEE 0  DC -12
VCC  8 0  DC 12
X1  3 1 6  SUB1
CLOAD  3 4  0.75PF
RC1  3 8  RMOD  1K
RBIAS  7 8  2K
X2  4 0 6  SUB1 
X3  6 7  SUB2 
RC2  4 8  RMOD  1K
VGOAL3  VGOAL3 0  PWLDC( -0.25V 12 -0.2V 11.97 -0.15V 11.9 -0.1V 11 
+ -0.05V 9.2 0V 5.6 0.05V 2.1 0.1V 0.8  0.15V 0.4 2V 0.31 2.5V 0.3)
RGOAL3  VGOAL3 0  -1
.MODEL QNL NPN(BF=80 RB=100 CCS=0.2PF TF=0.03NS TR=0.6NS
+              CJE=0.3PF CJC=0.2PF)
.MODEL RMOD R (TC1=0.003)
*........ PART II. ANALYSIS, OUTPUT and CONTROL CARDS .............
.TRAN  5NS 80NS 1NS 1.5NS  CALLV  SAVEV
.MEASURE  TRAN  MAX_TR_V3  MAX  V(3)
.MEASURE  TRAN  MIN_TR_V3  MIN  V(3)
.MEASURE  TRAN  FALL_1_TR_V3  WAVE  V(3)
+        FALL=1 VAL0=MIN_TR_V3 VAL1=MAX_TR_V3
.MEASURE TRAN  DEL_V3_V1  DELAY     V(1) RISE=1 VAL=0 
+        TARG=V(3) FALL=1 VAL0=MIN_TR_V3 VAL1=MAX_TR_V3
.MEASURE  TRAN  CR_V3_V4_3  CROSS  V(3) V(4) OCCUR=3
.DC  VIN  -0.25 0.25 0.05  CALLV 
.SNS  DC  V(3) V(X1.25) V(Q.X1.Q2#B) I(VIN) I(VCC) I(VEE) V(4)
+  TO
+ VIN(DC) RBIAS(RES) R.X1.RS2(RES)  QNL(RB)
+  TEMP GMIN  ARG=-0.25V 
.MEASURE  DC  ERR_V3_VGOAL3  ERR  V(VGOAL3) V(3)
.AC  DEC 10 100KHZ 10GHZ  CALLV  UIC
.MEASURE  AC  MAX_VM3  MAX  VM(3)
.MEASURE  AC  MIN_VM3  MIN  VM(3) TO=1MHZ
.MEASURE  AC  RISE_1_VM3  WAVE  VM(3)
+        COEF=0.01 RISE=1 VAL0=MIN_VM3 VAL1=MAX_VM3
.MODIF  RBIAS(RES)=2.5K  CLOAD(CAP)=0.75PF
+MODIF LOOP=5 STOP DEL_V3_V1 LE 2NS RBIAS(RES)*=0.9 CLOAD(CAP)*=0.6
.OPTIONS RELTOL=0.01 ABSTOL=1N VNTOL=50U ITL1=100 ITL2=50 ITL4=10 
+    ACCT NOMOD
.END
