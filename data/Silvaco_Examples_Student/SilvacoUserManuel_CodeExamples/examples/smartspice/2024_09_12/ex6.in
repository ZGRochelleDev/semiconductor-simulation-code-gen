   EXAMPLE6: TWO-BIT BJT ADDER
*
.GLOBAL VCC
.SUBCKT NAND 1 2 3 
Q1  9 5 1  QNL
Q2  9 5 2  QNL
Q3  6 9 8  QNL
Q4  7 6 10 QNL
Q5  3 8 0  QNL
D1  0 1  DIOD
D2  0 2  DIOD
D3  10 3 DIOD
R1  VCC 5  4K
R2  VCC 6  1.6K
R3  8   0  1K
R4  VCC 7  130
.ENDS NAND
*************  INA INB CIN OUT COUT
.SUBCKT ONEBIT  1   2   3   4   5 
X1  1  2  7   NAND
X2  1  7  8   NAND
X3  2  7  9   NAND
X4  8  9  10  NAND
X5  3  10 11  NAND
X6  3  11 12  NAND
X7  10 11 13  NAND
X8  12 13 4   NAND
X9  11 7  5   NAND
.ENDS ONEBIT
.SUBCKT TWOBIT  INA1 INB1 INA2 INB2  BIT1 BIT2  CIN COUT 
X1  INA1  INB1  CIN  BIT1 10   ONEBIT
X2  INA2  INB2  10   BIT2 COUT ONEBIT
.ENDS TWOBIT
VINA1  1 0  PULSE( 0 3 0 10NS 10NS 10NS 50NS)
VINB1  2 0  PULSE( 0 3 0 10NS 10NS 20NS 100NS)
VINA2  3 0  PULSE( 0 3 0 10NS 10NS 40NS 200NS)
VINB2  4 0  PULSE( 0 3 0 10NS 10NS 80NS 400NS)
X1   1 2 3 4   BIT1 BIT2  0  COUT    TWOBIT
RB1  BIT1 0  1K
RB2  BIT2 0  1K
RCOUT   COUT 0  1K
VCC  VCC 0  DC 5
.MODEL QNL NPN(BF=75 RB=100 CJE=1PF CJC=3PF)
.MODEL DIOD D
.TRAN 1NS 400NS
.MEASURE TRAN MAX_BIT1 MAX  V(BIT1)
.MEASURE TRAN DEL_BIT1_INA1_RISE  DELAY V(1) RISE=4 VAL=1.5
+  TARG=V(BIT1) RISE=2 VAL0=0 VAL1=MAX_BIT1
.MEASURE TRAN DEL_BIT1_INA1_FALL  DELAY V(1) FALL=4 VAL=1.5
+  TARG=V(BIT1) FALL=2 VAL0=0 VAL1=MAX_BIT1
.OPTIONS  ACCT  RELTOL=0.05  NOMOD
.END
