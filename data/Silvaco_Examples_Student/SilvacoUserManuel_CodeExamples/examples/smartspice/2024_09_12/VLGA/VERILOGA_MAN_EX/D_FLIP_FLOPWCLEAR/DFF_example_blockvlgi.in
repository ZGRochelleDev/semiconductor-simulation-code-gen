* DFF with Clear SPICE primitive

.verilog "dff_cl.va"

* Options
.OPTIONS  ACCT  NOMOD

*{ Parameters begin -------------------------------------

* PSU Voltage
.param VDD=3.3

* Waveform Rise and Fall Times  
.param SLOPE=0.09N 

* DFF Shift Register Rise and Fall Times
.param SLOPE_VAL=308p

*} Parameters end ---------------------------------------

* Globals
.global VDD  GND 

*{ Subcircuit begin ---------------------------------

* Inverter Subcircuit 
.subckt INVERTER IN OUT
 MP1 OUT IN GND GND NM L=LN W=WN 
 MP2 OUT IN VDD VDD PM L=LP W=WP 
.ends INVERTER

*} Subcircuit end -----------------------------------

*{ Voltage sources begin -------------------------------------

* Power Supply
V1 VDD 0 DC 3.3

* Clear Signal 
VCLR CLR 0 PWL(0 0 1N 0 1.09N VDD 102N VDD 102.09N 0 ) 

* Clock Signal
VCLK CLK 0 PULSE(0 VDD 5N SLOPE SLOPE 4.91N 10N)   

* Data Signal
VDATA DATA 0 PULSE(0 VDD 3N SLOPE SLOPE 9.91N 20N)   

*} Voltage sources end ----------------------------------------

*{ X-calls begin ---------------------------------------------------

* Data Driver
XDATA1 DATA NET1    INVERTER  PARAMS: WP=7U  LP=0.4U WN=5U  LN=0.4U
XDATA2 NET1 DATA_VA INVERTER  PARAMS: WP=7U  LP=0.4U WN=5U  LN=0.4U

* Clock Driver
XCLK1  CLK  NET2    INVERTER  PARAMS: WP=7U  LP=0.4U WN=5U  LN=0.4U
XCLK2  NET2 CLK_VA  INVERTER  PARAMS: WP=28U LP=0.4U WN=20U LN=0.4U

* Clear Driver
XCLR1  CLR  NET3    INVERTER  PARAMS: WP=7U  LP=0.4U WN=5U  LN=0.4U
XCLR2  NET3 CLR_VA  INVERTER  PARAMS: WP=28U LP=0.4U WN=20U LN=0.4U

* Output Inverters to create Qbar
XQ0    Q0   O0_VA   INVERTER  PARAMS: WP=7U  LP=0.4U WN=5U  LN=0.4U
XQ1    Q1   O1_VA   INVERTER  PARAMS: WP=7U  LP=0.4U WN=5U  LN=0.4U
XQ2    Q2   O2_VA   INVERTER  PARAMS: WP=7U  LP=0.4U WN=5U  LN=0.4U
XQ3    Q3   O3_VA   INVERTER  PARAMS: WP=7U  LP=0.4U WN=5U  LN=0.4U
XQ4    Q4   O4_VA   INVERTER  PARAMS: WP=7U  LP=0.4U WN=5U  LN=0.4U
XQ5    Q5   O5_VA   INVERTER  PARAMS: WP=7U  LP=0.4U WN=5U  LN=0.4U
XQ6    Q6   O6_VA   INVERTER  PARAMS: WP=7U  LP=0.4U WN=5U  LN=0.4U
XQ7    Q7   O7_VA   INVERTER  PARAMS: WP=7U  LP=0.4U WN=5U  LN=0.4U

*} X-calls end -----------------------------------------------------

*{ Capacitive loading begin ------

CL0 O0_VA 0  '0.02PF' 
CL1 O1_VA 0  '0.02PF' 
CL2 O2_VA 0  '0.02PF' 
CL3 O3_VA 0  '0.02PF' 
CL4 O4_VA 0  '0.02PF' 
CL5 O5_VA 0  '0.02PF' 
CL6 O6_VA 0  '0.02PF' 
CL7 O7_VA 0  '0.02PF' 

*} Capacitive loading end --------

*{ Verilog-A instances DFF begin ------------------------------------

YVLG0 DATA_VA CLK_VA CLR_VA Q0 DFF_CL  FO=2.03085 SLOPE_IN=SLOPE_VAL 
YVLG1 Q0      CLK_VA CLR_VA Q1 DFF_CL  FO=2.03085 SLOPE_IN=SLOPE_VAL 
YVLG2 Q1      CLK_VA CLR_VA Q2 DFF_CL  FO=2.03085 SLOPE_IN=SLOPE_VAL 
YVLG3 Q2      CLK_VA CLR_VA Q3 DFF_CL  FO=2.03085 SLOPE_IN=SLOPE_VAL 
YVLG4 Q3      CLK_VA CLR_VA Q4 DFF_CL  FO=2.03085 SLOPE_IN=SLOPE_VAL 
YVLG5 Q4      CLK_VA CLR_VA Q5 DFF_CL  FO=2.03085 SLOPE_IN=SLOPE_VAL 
YVLG6 Q5      CLK_VA CLR_VA Q6 DFF_CL  FO=2.03085 SLOPE_IN=SLOPE_VAL 
YVLG7 Q6      CLK_VA CLR_VA Q7 DFF_CL  FO=1.34995 SLOPE_IN=SLOPE_VAL 

*} Verilog-A instances DFF end --------------------------------------

* Transistor Model Parameters (Default Model)
.model  PM  PMOS  LEVEL=49
.model  NM  NMOS  LEVEL=49

* Analysis
.tran  1P  120N

* Plotting

.option
+ block_vlgi=1

.save V(O1_VA) V(O0_VA) V(CLR_VA) V(CLK_VA) V(O7_VA) V(O5_VA) V(O6_VA) V(O3_VA)
+ V(O4_VA) V(O2_VA)

.end
