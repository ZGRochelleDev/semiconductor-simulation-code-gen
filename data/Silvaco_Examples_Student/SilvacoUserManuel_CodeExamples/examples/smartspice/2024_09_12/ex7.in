   EXAMPLE7. TWO-BIT MOSFET ADDER
*
.GLOBAL VSS
.SUBCKT NAND 1 2 3  
M1  3  1 VSS VSS   MOSP W=10U  L=1.3U  
M2  3  2 VSS VSS   MOSP W=10U  L=1.3U 
M3  3  1 10   0    MOSN W=5U   L=1.3U
M4  10 2  0   0    MOSN W=5U   L=1.3U
C1  3  0  55FF
.ENDS NAND
*************  INA INB CIN OUT COUT
.SUBCKT ONEBIT  1   2   3   4   5   
X1  1  2  7   NAND
X2  1  7  8   NAND
X3  2  7  9   NAND
X4  8  9  10  NAND
X5  3  10 11  NAND
X6  3  11 12  NAND
X7  10 11 13  NAND
X8  12 13 4   NAND
X9  11 7  5   NAND
.ENDS ONEBIT
.SUBCKT TWOBIT  INA1 INB1 INA2 INB2  BIT1 BIT2  CIN COUT  
X1  INA1  INB1  CIN  BIT1 10    ONEBIT
X2  INA2  INB2  10   BIT2 COUT  ONEBIT
.ENDS TWOBIT
VINA1  1 0  PULSE( 2.8 0.4 0 1NS 1NS 14NS 30NS)
VINB1  2 0  PULSE( 2.8 0.4 0 1NS 1NS 29NS 60NS)
VINA2  3 0  PULSE( 2.8 0.4 0 1NS 1NS 14NS 30NS)
VINB2  4 0  PULSE( 2.8 0.4 0 1NS 1NS 29NS 60NS)
VINCIN CIN 0 PULSE( 0.4 2.8 0 1NS 1NS 6.5NS 15NS)
VSS  VSS 0  DC 4.5
X1   1 2 3 4   BIT1 BIT2  CIN  COUT   TWOBIT 
.MODEL MOSP PMOS(LEVEL=3 VTO=-0.8  TOX=0.41E-7 UO=150    LD=0.14U 
+ KP=14U    ETA=0.014 THETA=0.042  VMAX=4.8E4  NFS=1E11  NSS=2E10
+ NSUB=2E16 JS=0.1M   GAMMA=0.68   KAPPA=0.24  XJ=0.45U  RSH=85 
+ PB=0.7    CJ=0.2M   CJSW=0.2N  )
.MODEL MOSN NMOS(LEVEL=3 VTO=0.7   TOX=0.41E-7 UO=500    LD=0.1U  
+ KP=28U    ETA=0.01  THETA=0.042  VMAX=1.9E5  NFS=1E11  NSS=2E10
+ NSUB=9E16 JS=0.1M   GAMMA=1.52   KAPPA=0.24  XJ=0.45U  RSH=85 
+ PB=0.7    CJ=0.2M   CJSW=0.2N  )
*
.TRAN 1NS 60NS  
.MEASURE TRAN  MAX_BIT1  MAX  V(BIT1)
.MEASURE TRAN  MAX_COUT1  MAX  V(X1.10)
.MEASURE TRAN  DEL_BIT1_CIN  DELAY 
+       V(CIN)  FALL=1  VAL=1.4
+  TARG=V(BIT1) FALL=1  VAL0=0  VAL1=MAX_BIT1
.MEASURE TRAN  FALL_BIT2  WAVE
+  V(BIT2)  FROM=40NS FALL=1 VAL0=0 VAL1=4.5
.MEASURE TRAN  FALL_BIT1  WAVE
+  V(BIT1)  FROM=40NS FALL=1 VAL0=0 VAL1=4.5
.MEASURE TRAN  WIDTH_COUT1  DELAY
+      V(X1.10) RISE=1 VAL0=0 VAL1=MAX_COUT1
+ TARG=V(X1.10) FALL=1 VAL0=0 VAL1=MAX_COUT1
.OPTIONS ACCT NOMOD
.END
