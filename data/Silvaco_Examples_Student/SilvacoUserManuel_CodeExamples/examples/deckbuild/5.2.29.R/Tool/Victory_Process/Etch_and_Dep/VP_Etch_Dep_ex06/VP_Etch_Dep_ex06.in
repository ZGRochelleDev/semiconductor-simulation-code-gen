# (c) Silvaco Inc., 2024

# Deep Reactive Ion Etch example 

# Required Modules
#  Victory Process
#   -3D Physical Etch & Deposit

# Example Name
Set name="VP_Etch_Dep_ex06" 

####### PROCESS SIMULATION ####### 
go victoryprocess simflag="-P 4"

# Define the simulation domain, the wafer material and the simulation mesh resolution
# and start the simulation in 3D mode
INIT material="silicon" depth=1.0 gasHeight=0.5 from="0, 0" to="0.50, 0.15" \
 resolution="0.05,0.05,0.05" flow.dim=3d

# Define simple mask polygon
SPECIFYMASKPOLY maskid=1 istransparent=false p1="-1, -1" p2="-1, 1" p3="0.25, 1" p4="0.25, -1"

# Deposit 250nm of resist
DEPOSIT conformal thickness=0.25 material="photoresist" maskID=1 angle=75

# EXPORT - structures generated using the EXPORT statement are for 
# visualization only
EXPORT name="$'name'_Start.str"

# ======================
# Set up RIE etch models
# ======================

# Define material properties : 
# The material specific property : 
# rate
# is required by the surface reaction model in this example 
ETCHDEPOPROPERTIES name="rieetcherrate" \
 material="silicon" rate=5.50 \
 material="photoresist" rate=0.02 \
 material="polymer" rate=0.10 


# Define the etch flux model parameters:
# Model parameters : 1) Etch beam width
# 2) Ratio ion / neutral
# Model dependencies: 1) Angle between surface normal and beam
# 2) Surface material
FLUX name="rieetcherflux" \
 primaryfunctionname="rieflux" \
 primaryparam="60.0" primaryparam="0.05" \
 primarydepend="theta" primarydepend="surfacematerial"

# Define the surface reaction
# In this example we do not define a surface reaction but use a predefined model :
# fluxdependent - rate is linear proportional to the flux
# for the surface reaction

# Define the topography model to combine surface reaction + transport (flux) models
TOPOGRAPHYMODEL name="rieetchmodel" reactionmodelname="fluxdependent" fluxmodelname="rieetcherflux"

# ============================================
# Run the iterative passivation + etch process
# ============================================

# Perform the DRIE cycles - 2 used here
# A LOOP could be used here. Sequential statements are used for clarity instead.

#### First Cycle ####

# Passivation
# -----------
# Note : We assume that the deposition process is conformal
# You may improve the DRIE model by applying a more sophisticated
# model for the deposition step here
DEPOSIT model="conformal" material="polymer" depositionrate=0.07 time=5 seconds

# EXPORT - structures generated using the EXPORT statement are for 
# visualization only
EXPORT name="$'name'_PreEtch_Cycle_01.str"
EXPORT 2D ycenter structure=$'name'_PreEtch_Cycle_01_2d.str

# Etching
# -------
# Note : In order to get good numerical accuracy we recomment to use
# maxCFL <= 1.
ETCH model="rieetchmodel" materialproperties="rieetcherrate" time=10 seconds maxCFL=1.0

# EXPORT - structures generated using the EXPORT statement are for 
# visualization only
EXPORT name="$'name'_PostEtch_Cycle_01.str"
EXPORT 2D ycenter structure=$'name'_PostEtch_Cycle_01_2d.str

#### Second Cycle ####
# No comments as same as first cycle)

DEPOSIT model="conformal" material="polymer" depositionrate=0.07 time=5 seconds
EXPORT name="$'name'_PreEtch_Cycle_02.str"
EXPORT 2D ycenter structure=$'name'_PreEtch_Cycle_02_2d.str

ETCH model="rieetchmodel" materialproperties="rieetcherrate" time=10 seconds maxCFL=1.0
EXPORT name="$'name'_PostEtch_Cycle_02.str"
EXPORT 2D ycenter structure=$'name'_PostEtch_Cycle_02_2d.str

victoryvisual \
 $'name'_Start.str \
 $'name'_PostEtch_Cycle_01.str \
 $'name'_PostEtch_Cycle_02.str

QUIT
