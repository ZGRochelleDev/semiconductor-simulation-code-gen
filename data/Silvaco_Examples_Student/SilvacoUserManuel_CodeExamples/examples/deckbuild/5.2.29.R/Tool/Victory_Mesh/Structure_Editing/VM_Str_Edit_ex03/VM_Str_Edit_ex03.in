# (c) Silvaco Inc., 2024

# Rotating a structure file (ROTATE)

# Required Modules
#  Victory Process
#   -Core Simulator 
#  Victory Mesh
#   -2D Meshing
#   -2D Solid Modeling

# Example Name
Set name=“VM_Str_Edit_ex03” 

####### PROCESS SIMULATION ####### 
# Makes simple structure with MOS trench and junction
GO victoryprocess simflags="-P 4"

INIT material=silicon from=1 to=2 depth=2 \
resolution=0.01,0.01 meshdepth=2 gasheight=2.5 \
flow.dim=2D_xz c.phosphorus=1E14

ETCH min material=silicon thickness=1.1 right.to.x=1.8
ETCH conformal thickness=0.2 material=silicon right.to.x=1.8

DEPOSIT conformal material=oxide thickness=0.01
ETCH material=oxide above=0
DEPOSIT material=polysilicon thickness=0 max

DOPING boron conc=1e20 gauss.sigma=0.05 peak=2

# Save simulation status for remeshing
SAVE name=$'name'_VP_sv

# Single half trench cell - no rotate. Including
# interface and junction refinement using delaunay mesh
GO victorymesh simflags="-P 4"

LOAD in="$'name'_VP_sv"

REMESH delaunay 

# Base mesh
REFINE max.size=0.05 regions="*"

# Interface refinement but increase the distance over which
# the refinement grades over
REFINE max.interface.size=0.01 max.interface.distance=0.5 grading=linear

# Junction refinement
REFINE max.junction.size=0.02 grading="linear"

SAVE out="$'name'_VM_NoRotate"

# Device rotated by 180 deg around the bottom left corner
# Interface and junction refinement using delaunay mesh
GO victorymesh simflags="-P 4"

LOAD in="$'name'_VP_sv"

# Device rotated by 180 deg around the bottom left corner
# Structure manipulation before remesh
ROTATE angle=180 point.a="1,2"

REMESH delaunay 

# Base mesh
REFINE max.size=0.05 regions="*"

# Interface refinement but increase the distance over which
# the refinement grades over
REFINE max.interface.size=0.01 max.interface.distance=0.5 grading=linear

# Junction refinement
REFINE max.junction.size=0.02 grading="linear"

SAVE out="$'name'_VM_Rotate"

victoryvisual \
$'name'_VM_NoRotate.str \
$'name'_VM_Rotate.str

QUIT
