# (c) Silvaco Inc., 2024

# Naming regions to localise refinement (TAG)

# Required Modules
#  Victory Process
#   -Core Simulator 
#  Victory Mesh
#   -2D Meshing

# Example Name
Set name=“VM_Meshing_ex04” 

####### PROCESS SIMULATION ####### 
# Makes simple block of material with alternating
# layers of GaN and AlGaN
GO victoryprocess simflags="-P 4"

INIT material=GaN from=0 to=2 depth=0.2 \
resolution=0.01,0.01 meshdepth=2 gasheight=1.5 \
flow.dim=2D_xz

DEPOSIT material="algan" thickness=0.1 max

DEPOSIT material="gan" thickness=0.2 max
DEPOSIT material="algan" thickness=0.1 max

DEPOSIT material="gan" thickness=0.2 max
DEPOSIT material="algan" thickness=0.1 max

# Save simulation status for remeshing
SAVE name=$'name'_VP_sv

# No TAG used, all layers of the same material type will
# have the same refinement
GO victorymesh simflags="-P 4"

LOAD in="$'name'_VP_sv"

REMESH delaunay 

# Refinement in all GaN layers
# Good practice to order refinment statements in
# order of decreasing mesh size
REFINE max.size=0.05 regions="material:GaN"

# Refinement in all AlGaN layers
REFINE max.size=0.025 regions="material:AlGaN"

SAVE out="$'name'_VM_Del_Material"

# Use TAG to give regions different names so regions of the 
# same material can be treated individually
# NOTE: the name used on the TAG statement can then also be
# used in Victory Device to localise model specification in
# the device simulation
GO victorymesh simflags="-P 4"

LOAD in="$'name'_VP_sv"

# Specify point co-ordinate inside material region
# and provide a user specified name
# Here we are just tagging the three AlGaN layers
TAG point="1,-0.05" value="AlGaN_Bottom"
TAG point="1,-0.35" value="AlGaN_Middle"
TAG point="1,-0.65" value="AlGaN_Top"

REMESH delaunay 

# Refinement in all GaN layers
REFINE max.size=0.05 regions="material:GaN"

# Different refinement in different AlGaN layers using
# the TAG name to localise the refinement
REFINE max.size=0.025 regions="AlGaN_Bottom"
REFINE max.size=0.0125 regions="AlGaN_Middle"
REFINE max.size=0.00625 regions="AlGaN_Top"

SAVE out="$'name'_VM_Del_Tag"

victoryvisual \
 $'name'_VM_Del_Material.str:$'name'_Mesh.set \
 $'name'_VM_Del_Tag.str:$'name'_Mesh.set

QUIT
