# (c) Silvaco Inc., 2024

# Refining in user specified shapes (SHAPE)

# Required Modules
#  Victory Process
#   -Core Simulator 
#  Victory Mesh
#   -2D Meshing

# Example Name
Set name=“VM_Meshing_ex03” 

####### PROCESS SIMULATION ####### 
# Makes simple block of silicon with an analytic gaussian profile
# to use in the Victory Mesh example
GO victoryprocess simflags="-P 4"

INIT material=silicon from=0 to=2 depth=1 \
resolution=0.01,0.01 meshdepth=2 gasheight=1 \
c.phosphorus=1E13 

# No LINE statement specified for volume (doping) mesh
# therefore geometry mesh (from RESOLUTION) used 

MASK outside.x="0.8,1.2"
DOPING boron conc=1E20 peak=0 gauss.sigma=0.05 lateral.sigma=0.05
STRIP

# Save simulation status for remeshing
SAVE name=$'name'_VP_sv

# Adds a box of refinement over the doping well
GO victorymesh simflags="-P 4"

# Specify shape (here "cube" is used) that will be called
# and used in the refinement statement
# coordinates define upper left and lower right corner
# of the box. 
CUBOID from="0.4,0" to="1.6,0.45" out="my_shape"

LOAD in="$'name'_VP_sv"

REMESH delaunay 

# base mesh
REFINE max.distance=0.001 regions="*"
REFINE max.size=0.1 regions="*"

# Calls in shape defined and specifies mesh size within the shape
REFINE max.shape.size=0.01 shape="my_shape"

SAVE out="$'name'_VM_shape"

victoryvisual \
$'name'_VM_shape.str:$'name'_VM_SHAPE.set

QUIT
