# (c) Silvaco Inc., 2024

# Conformal refinement versus delaunay refinement

# Required Modules
#  Victory Process
#   -Core Simulator 
#  Victory Mesh
#   -2D Meshing

# Example Name
Set name=“VM_Meshing_ex06” 

####### PROCESS SIMULATION ####### 
# Makes simple structure with MOS trench and junction
GO victoryprocess simflags="-P 4"

INIT material=silicon from=1 to=2 depth=2 \
resolution=0.01,0.01 meshdepth=2 gasheight=2.5 \
flow.dim=2D_xz c.phosphorus=1E14

ETCH min material=silicon thickness=1.1 right.to.x=1.8
ETCH conformal thickness=0.2 material=silicon right.to.x=1.8

DEPOSIT conformal material=oxide thickness=0.01
ETCH material=oxide above=0
DEPOSIT material=polysilicon thickness=0 max

DOPING boron conc=1e20 gauss.sigma=0.05 peak=2

# Save simulation status for remeshing
SAVE name=$'name'_VP_sv

# Interface and junction refinement using delaunay mesh
GO victorymesh simflags="-P 4"

LOAD in="$'name'_VP_sv"

REMESH delaunay 

# Base mesh
REFINE max.size=0.05 regions="*"

# Interface refinement but increase the distance over which
# the refinement grades over
REFINE max.interface.size=0.01 max.interface.distance=0.5 grading="linear"

# Junction refinement
REFINE max.junction.size=0.02 grading="linear"

SAVE out="$'name'_VM_Delaunay"

# Repeat but now use conformal meshing
GO victorymesh simflags="-P 4"

LOAD in="$'name'_VP_sv"

REMESH conformal simplex.minimal spacing=0.2

# Base mesh - anisotripic mesh now permitted - 
# different x-z dimensions can be used
REFINE max.size="0.05,0.05" regions="*"

# Interface refinement but increase the distance over which
# the refinement grades over.
# Double value of max.size compared to delaunay to give comparable refinement.
# Delaunay bounds a triangle’s circumradius. Conformal bounds on the dimensions
# of a triangle’s axis-aligned bounding box.
REFINE max.interface.size="0.02, 0.02" max.interface.distance=0.5 grading=linear

# Junction refinement
REFINE max.junction.size="0.04,0.04" grading="linear"

SAVE out="$'name'_VM_Conformal"

victoryvisual \
$'name'_VM_Delaunay.str:$'name'_Mesh.set \
$'name'_VM_Conformal.str:$'name'_Mesh.set

QUIT
