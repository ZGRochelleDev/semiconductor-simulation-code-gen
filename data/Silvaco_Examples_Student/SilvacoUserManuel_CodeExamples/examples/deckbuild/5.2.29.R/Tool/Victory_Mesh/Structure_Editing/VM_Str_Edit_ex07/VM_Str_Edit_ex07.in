# (c) Silvaco Inc., 2024

# Slicing a 3D region into multiple sections (SLICE)

# Required Modules
#  Victory Process
#   -Core Simulator 
#  Victory Mesh
#   -2D Meshing
#   -3D Meshing

# Example Name
Set name=“VM_Str_Edit_ex07” 

####### PROCESS SIMULATION ####### 
# Structure with planar MOS gate.
GO victoryprocess simflags="-P 4"

INIT material=silicon from=1.5,1.5 to=2,2 depth=2 \
resolution=0.01 meshdepth=2 gasheight=0.5 \
c.phosphorus=1E14 flow.dim=3D

DOPING boron conc=1e20 gauss.sigma=0.05 peak=2

# Mask for the MOS gate. Mask extends slightly outside the 
# simulation domain - good practice to not align with domain
# unless needed
SPECIFYMASKPOLY rect lb="1.8,0" rt="2.2,1.7" mask=gate
DEPOSIT max material=oxide thickness=0.01 mask=gate
DEPOSIT max material=polysilicon thickness=0.05 mask=gate

# Save simulation status for remeshing
SAVE name=$'name'_VP_3D_sv 

# Delaunay remesh on 3D structure using
# interface and junction refinement with 
# silicon substrate sliced into two.
GO victorymesh simflags="-P 4"

LOAD in="$'name'_VP_3D_sv"

# slice substrate into two regions (of silicon)
SLICE from="1.65,1.5,-1" to="2,1.85,1"
# give the two silicon regions different names
TAG point="1.55,1.55,0.5" value="bulk"
TAG point="1.85,1.55,0.5" value="active"

REMESH delaunay axis.aligned

# Base mesh (different values in the different regions of silicon
REFINE max.size=0.05 regions="bulk" 
REFINE max.size=0.025 regions="active" 
REFINE max.size=0.01 regions="material:polysilicon" 
REFINE max.size=0.005 regions="material:oxide" 
REFINE max.distance=0.0001 

# Interface refinement
REFINE max.interface.size=0.005 max.interface.distance=0.25 \
 max.interface.distance.size=0.05 grading="linear" 

# Junction refinement
REFINE max.junction.size=0.02 max.junction.distance.size=0.05 \
 grading="linear"

SAVE out="$'name'_VM_3D" 

victoryvisual \
 $'name'_VM_3D.str:$'name'_VM_3D_Mesh.set

QUIT
