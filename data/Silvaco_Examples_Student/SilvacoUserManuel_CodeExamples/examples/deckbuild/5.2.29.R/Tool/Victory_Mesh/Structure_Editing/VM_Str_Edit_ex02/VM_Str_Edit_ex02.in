# (c) Silvaco Inc., 2024

# Stretching a structure file (STRETCH)

# Required Modules
#  Victory Process
#   -Core Simulator 
#  Victory Mesh
#   -2D Meshing
#   -2D Solid Modeling

# Example Name
Set name=“VM_Str_Edit_ex02” 

####### PROCESS SIMULATION ####### 
# Makes simple structure with MOS trench and junction
GO victoryprocess simflags="-P 4"

INIT material=silicon from=1 to=2 depth=2 \
resolution=0.01,0.01 meshdepth=2 gasheight=2.5 \
flow.dim=2D_xz c.phosphorus=1E14

ETCH min material=silicon thickness=1.1 right.to.x=1.8
ETCH conformal thickness=0.2 material=silicon right.to.x=1.8

DEPOSIT conformal material=oxide thickness=0.01
ETCH material=oxide above=0
DEPOSIT material=polysilicon thickness=0 max

DOPING boron conc=1e20 gauss.sigma=0.05 peak=2

# Save simulation status for remeshing
SAVE name=$'name'_VP_sv

# Single half trench cell - no stretch. Including
# interface and junction refinement using delaunay mesh
GO victorymesh simflags="-P 4"

LOAD in="$'name'_VP_sv"

REMESH delaunay 

# Base mesh
REFINE max.size=0.05 regions="*"

# Interface refinement but increase the distance over which
# the refinement grades over
REFINE max.interface.size=0.01 max.interface.distance=0.5 grading=linear

# Junction refinement
REFINE max.junction.size=0.02 grading="linear"

SAVE out="$'name'_VM_NoStretch"

# Substrate stretched by an additional 10um
# Interface and junction refinement using delaunay mesh
GO victorymesh simflags="-P 4"

LOAD in="$'name'_VP_sv"

# Stretch substrate out by additional 10um
# Structure manipulation before remesh
STRETCH axis="z" offsets=1.5 widths=10

REMESH delaunay 

# Base mesh
REFINE max.size=0.05 regions="*"

# Interface refinement but increase the distance over which
# the refinement grades over
REFINE max.interface.size=0.01 max.interface.distance=0.5 grading=linear

# Junction refinement
REFINE max.junction.size=0.02 grading="linear"

SAVE out="$'name'_VM_Stretch"

victoryvisual \
$'name'_VM_NoStretch.str \
$'name'_VM_Stretch.str

QUIT
