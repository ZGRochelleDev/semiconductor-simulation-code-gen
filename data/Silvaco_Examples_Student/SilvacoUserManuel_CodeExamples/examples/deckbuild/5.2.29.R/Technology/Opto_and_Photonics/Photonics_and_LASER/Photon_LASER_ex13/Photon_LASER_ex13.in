# (c) Silvaco Inc., 2024

# Front Mirror with Tuning Air Gap and DBR Rear Mirror 

# Required Modules
#  Atlas
#   -Blaze
#   -Laser

# Example Name
SET name=“Photon_LASER_ex13” 

####### DEVICE SIMULATION ####### 
GO atlas simflags="-P 4"

SET hbar         = 6.582173483e-16
SET hc           = 1.2407
SET phote        = 1.4
SET lambda       = $"hc"/$"phote"
SET dbr_index1   = 3.70
SET dbr_index2   = 3.18
SET dbr_thick1   = 0.25*$"lambda"/$"dbr_index1"
SET dbr_thick2   = 0.25*$"lambda"/$"dbr_index2"
SET dbr_ncyc     = 40
SET cavity_start = 0.5*$"dbr_ncyc"*($"dbr_thick1"+$"dbr_thick2")

# Location of front mirror
SET front_mirror = 100.6
# Reflectivity of front mirror
SET rf           = 0.0
# End of cavity
SET cavend       = 100.0

#============================================================================
Title Simulation of a Distributed Bragg Reflector Laser
#============================================================================

# SECTION 1: Mesh Specification 

MESH auto
X.MESH location=-0.10 s=0.05
X.MESH location= 0.00 s=0.02
X.MESH location= 0.45 s=0.005
X.MESH location= 0.55 s=0.005
X.MESH location= 0.90 s=0.02
X.MESH location= 1.00 s=0.002
X.MESH location= 1.02 s=3.0
X.MESH location= 9.90 s=3.0
X.MESH location= 10.0 s=0.05

Y.MESH location= 0    s=0.02
Y.MESH location= 4.0  s=0.02
Y.MESH location= 10   s=1
Y.MESH location= $"cavend"   s=1

# SECTION 2: Region Specification 

REGION num=1 name=barr material=AlGaAs x.comp=0.3 \
       x.min=-0.10, x.max=0.45,  y.min=0 
REGION num=2 name=well material=GaAs              \
       x.min= 0.45, x.max=0.55   y.min=0
REGION num=3 name=barr material=AlGaAs x.comp=0.3 \
       x.min= 0.55, x.max=1.00,  y.min=0 
REGION num=4 name=subs material=GaAs   \
       x.min= 1.00, x.max=10.0,  y.min=0  

DBR y.start=0 half.cyc=$"dbr_ncyc" thick1=$"dbr_thick1" thick2=$"dbr_thick2" \
    n1=2 n2=2 na1=1e-11 na2=1e-11 mat1=GaAs mat2=AlGaAs x2.comp=0.6 \
    name1=dbrname1 name2=dbrname2

ELECTRODE num=1 name=anode ^HELM \
          x.min=-0.1 x.max=0.00 y.min=0 y.max=$"cavend"
ELECTRODE num=2 name=base ^HELM \
          x.min=9.9 x.max=10.00 y.min=0 y.max=$"cavend" 

DOPING uniform region=1 p.type conc=1.e18
DOPING uniform region=3 n.type conc=1.e18
DOPING uniform region=4 n.type conc=1.e20

# SECTION 3: Material Specification

# We start by setting all materials to be non-gain in order to confine the
# gain to the well region
MATERIAL gain0=0 gain.broadening=0.005 imag.index=0 
MATERIAL fcn=1e-18 fcp=2e-18

# Here we specify the real and imaginary index for the DBR

MATERIAL name=dbrname1 real.index=$"dbr_index1" imag.index=0
MATERIAL name=dbrname2 real.index=$"dbr_index2" imag.index=0

# The barrier has a higher band gap than gain region, and the modes we are 
# interested in. So we set its imag.index=0

MATERIAL name=barr  imag.index=0

# Here we set the GAIN0 parameter for the standard gain model for GaAs well
# We set imag.index=0.0 because frequencies are much higher than phonon 
# contribution and the band-band transitions are taken care of by the gain 
# calculation for this region.

MATERIAL name=well gain0=6000 imag.index=0.0 eg300=1.39

# SECTION 4: MODELS Specification 

MODELS fermi spont srh auger 

# SECTION 5: Laser Mesh Specification 

LX.MESH l=0.0       s=0.05
LX.MESH l=0.45      s=0.01
LX.MESH l=0.55      s=0.01
LX.MESH l=1.0       s=0.05
LY.MESH l=0.00      s=0.5
LY.MESH l=3.80      s=0.5
LY.MESH l=10        s=4
LY.MESH l=$"cavend" s=4

LASER hybrid tol=0.4  itmax=6 maxch=2.5 maxtrap=10 prt.eval \
      cavity.start=$"cavity_start" hybrid.specres=0.5e-4 spec.name=comb \
      gainmod=1 absorption fcarrier losses=0 loss.diffraction \
      nmode=1 max.lmodes=12 ^helm.tm  \
      photon.energy=1.385 e.init=1.384 e.final=1.415 nspec=200 \
      rf=$"rf" rr=0.0 modal.reflectance frontmirror.loc=$"front_mirror"
# helm.which="SM"

# SECTION 6: Methods Specification 

method newton itlimit=15 maxtraps=20 

# SECTION 7: Output Specification

OUTPUT band.params ez.opt hz.opt las.loss las.longmodes \
       las.modalrefl las.refr las.dbrspec las.gainspec las.asespec

PROBE integrated name="ElecDen" n.conc \
      x.min=0.45 x.max=0.55 y.min=$"cavity_start" y.max=$"cavend"
PROBE integrated name="HoleDen" p.conc \
      x.min=0.45 x.max=0.55 y.min=$"cavity_start" y.max=$"cavend"
PROBE integrated name="gain" laser.gain \
      x.min=0.45 x.max=0.55 y.min=$"cavity_start" y.max=$"cavend"
PROBE integrated name="rcomb" radiative \
      x.min=0.0 x.max=1.0 y.min=$"cavity_start" y.max=$"cavend"
PROBE integrated name="auger" auger \
      x.min=0.0 x.max=1.0 y.min=$"cavity_start" y.max=$"cavend"

# SECTION 8: SOLVE Statements

SOLVE init
SOLVE prev

SOLVE name=anode vstep=0.5 vfinal=1.0
SOLVE name=anode vstep=0.1 vfinal=1.3

LOG outf= $'name'.log
SOLVE name=anode vstep=0.1 vfinal=1.4
SAVE spectrum=s1

contact name=anode current
SOLVE ianode=0.001

SOLVE name=anode istep=5e-5 ifinal=0.0011
SAVE spectrum=i1

SOLVE name=anode istep=5e-5 ifinal=0.0012
SAVE spectrum=i2

SOLVE name=anode istep=5e-5 ifinal=0.0013
SAVE spectrum=i3

SOLVE name=anode istep=5e-5 ifinal=0.0014
SAVE spectrum=i4

SOLVE name=anode istep=1e-4 ifinal=0.0016
SAVE spectrum=i5

SOLVE name=anode istep=1e-4 ifinal=0.0024
SAVE spectrum= $'name' outf= $'name'.str

system cp $'name'.spe $'name'_spe.log

victoryvisual \
$'name'.str:$'name'.set \
$'name'_spe.log:$'name'_spe.set \
$'name'_modal.log:$'name'_modal.set \
$'name'.log:$'name'_log.set

QUIT
