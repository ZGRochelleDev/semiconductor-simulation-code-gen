# (c) Silvaco Inc., 2024

# A 3D Elliptical Source with a Gaussian Optical Intensity

# Required Modules
#  Victory Device
#   -3D Simulator
#   -3D Optical

# Example Name
Set name="Photodiode_ex05"

####### DEVICE SIMULATION #######
GO victorydevice simflags="-P 4"

#### STRUCTURE SPECIFICATION ####
# Mesh Definition
Mesh three.d
#
X.MESH location=0.00 spacing=0.20
X.MESH location=5.00 spacing=0.20
#
Y.MESH location=0.00 spacing=0.05
Y.MESH location=5.00 spacing=0.50
Y.MESH location=10.0 spacing=0.10
#
Z.MESH location=0.00 spacing=0.20
Z.MESH location=5.00 spacing=0.20

# Regions and electrodes
REGION number=1 material=Silicon
#
ELECTRODE number=1 top
ELECTRODE number=2 substrate

# Device doping
DOPING uniform concentration=2e16 p.type
DOPING gaussian peak=0.00 characteristic=0.1 concentration=1e18 p.type 
DOPING gaussian peak=10.0 characteristic=0.1 concentration=1e18 n.type 

# Beam definition
BEAM number=1 x.origin=2.5 y.origin=-5.0 z.origin=2.5 \
 front.refl angle=90 wavelength=0.623 nx=21 nz=21 \ 
 xmean=0 zmean=0 x.gaussian z.gaussian x.sigma=1.0 \
 z.sigma=0.5 save.rays

#Solution solving		
METHOD newton carr=2
OUTPUT opt.intens
SOLVE initial 
SOLVE previous
SOLVE v1=-0.5
#
SOLVE b1=1.0
SAVE outf=$'name'_VD.str 

victoryvisual $'name'_VD.str:$'name'_VD_rays.set
victoryvisual $'name'_VD.str:$'name'_VD_pgr.set

QUIT
