# (c) Silvaco Inc., 2024

# PIN photodiode simulation example

# Required Modules
#  Victory Device
#   -2D Simulator
#   -2D Optical

# Example Name
Set name="Photodiode_ex01"

####### DEVICE SIMULATION #######
GO victorydevice simflags="-P 4"

#### STRUCTURE SPECIFICATION ####
# Mesh Definition
Mesh space.mult=4.00 
#
X.MESH location=0.00 spacing=0.25
X.MESH location=10.0 spacing=0.25
#
Y.MESH location=0.00 spacing=0.05
Y.MESH location=5.00 spacing=0.20
Y.MESH location=10.0 spacing=0.05

# Regions and electrodes
REGION number=1 material=Silicon
#
ELECTRODE number=1 name=anode x.min=0.0 x.max=10.0 y.max=0.0
ELECTRODE number=2 name=cathode bottom

# Device doping
DOPING uniform concentration=1e14 n.type
DOPING gaussian peak=0.00 characteristic=0.1 concentration=1e18 p.type 
DOPING gaussian peak=10.0 characteristic=0.1 concentration=1e18 n.type 

#### MATERIAL/PHYSICAL MODELS #### 
MATERIAL taup0=2.e-6 taun0=2.e-6 
MODELS srh auger conmob fldmob print 

#### SOLUTION SOLVING ####	
SOLVE initial 
SOLVE previous
SAVE outf=$'name'_VD.str 

# PIN diode DC solutions
# define a multi-spectral beam normal to top (y=0.0) surface
# this beam is as follows:
# beam #1, originating at (5.0,-1.0), propogating at an angle
# of 90 degrees, with a multi-spectral spectrum described in
# the file "spectrum.spec", sampled from 0.5 microns to 0.8 microns
# at 5 sample wavelengths.
#
#------------------------------------------------
BEAM number=1 x.origin=5.0 y.origin=-1.0 angle=90.0 power.file=spectrum.spec \
 wavel.start=0.5 wavel.end=0.8 wavel.num=5 save.rays

METHOD newton trap 
SOLVE initial 
SOLVE previous
SOLVE vcathode=0.1
SOLVE vcathode=0.5
SOLVE vcathode=1.0
SOLVE vcathode=2.0

# Current vs. intensity
# Ramping optical source intensity from 0 to 1 W/cm^2
LOG outf=$'name'_VD_II.log master
SOLVE b1=0.0 lit.step=0.1 nstep=76
SAVE outf=$'name'_VD_DC.str master
LOG off

# Light Turn-On Transient for PIN diode 
# Time Domain Photogeneration Simulation
# Optical source specification
BEAM number=1 x.origin=5.0 y.origin=-1.0 angle=90.0 wavelength=.623

SOLVE initial 
SOLVE previous
SOLVE vcathode=0.1
SOLVE vcathode=0.5 
SOLVE vcathode=1.0 
SOLVE vcathode=2.0 

# Solve dc with light
# ramp the light on in the time domain
# light turns on in 1ns then takes several ns to settle out
SOLVE b1=0
LOG	outf=$'name'_VD_Transient.log master
SOLVE b1=5 ramp.lit ramptime=1e-10 tstop=10e-9 tstep=1e-12
LOG off

# Diode small signal AC
# Small Signal AC Illumination
# Optical source specification
# define a multi-spectral beam normal to top (y=0.0) surface
# as follows:
# beam #1, origin at (2.5,-1.0), directed at an angle of 90 degrees,
# the beam spectrum is in the file power.spec, the spectrum is
# sampled from 0.5 microns to 0.8 microns with 4 samples.
#
BEAM number=1 x.origin=5.0 y.origin=-1.0 angle=90.0 power.file=power.spec \
 wavel.start=0.5 wavel.end=0.8 wavel.num=5

SOLVE initial 
SOLVE previous
SOLVE vcathode=0.1
SOLVE vcathode=0.5
SOLVE vcathode=1.0
SOLVE vcathode=2.0

# AC response
SOLVE b1=5
LOG outf=$'name'_VD_AC.log master
SOLVE b1=0.001 ss.phot ss.light=0.001 beam=1 freq=1e5 fstep=1.2 mult.f nfstep=101
LOG off

# Photodiode spectral response
# PIN device spectral response
# Optical source specification
BEAM number=1 x.origin=5.0 y.origin=-1.0 angle=90.0 wavelength=0.1

METHOD newton trap 
SOLVE initial
SOLVE previous
SOLVE vcathode=0.2

# SECTION 5: Spectral Response
LOG outf=$'name'_VD_SPECTRALRESPONSE.log master
SOLVE previous b1=1 beam=1 lambda=0.1 wstep=0.01 wfinal=0.8
LOG off
#
extract init infile="$'name'_VD_SPECTRALRESPONSE.log"
extract name="IQE" curve(ow."1", (apc."1"/spc."1")*100) outfile="$'name'_VD_QE.dat"


victoryvisual \
$'name'_VD.str:$'name'_VD.set \
$'name'_VD_II.log:$'name'_VD_II.set \
$'name'_VD_Transient.log:$'name'_VD_Transient.set

victoryvisual \
$'name'_VD_AC.log:$'name'_VD_AC.set \
$'name'_VD_SPECTRALRESPONSE.log:$'name'_VD_SPECTRALRESPONSE.set \
$'name'_VD_QE.dat

QUIT

