# (c) Silvaco Inc., 2024

# GaAs/AlGaAs Stripe Geometry Laser

# Required Modules
#  Atlas
#   -Blaze
#   -Laser

# Example Name
SET name=“Photon_LASER_ex03” 

####### DEVICE SIMULATION ####### 
GO atlas simflags="-P 2"
#
#    Illustration of lateral mode instability
#    in AlGaAs/GaAs strip geometry laser diode
#
#
MESH  space.mult=1.0 diag.flip
# 
X.MESH loc=-2.00 spac=0.5
X.MESH loc=0.00 spac=0.5
X.MESH loc=2 spac=0.2
X.MESH loc=7 spac=0.2
X.MESH loc=9.0 spac=0.5
X.MESH loc=11.00 spac=0.5
#
Y.MESH loc=0.0 spac=0.1
Y.MESH loc=0.5 spac=0.1
Y.MESH loc=0.55 spac=0.025
Y.MESH loc=0.7 spac=0.015
Y.MESH loc=0.75 spac=0.0075
Y.MESH loc=0.85 spac=0.0075
Y.MESH loc=0.9 spac=0.015
Y.MESH loc=1.05 spac=0.025
Y.MESH loc=1.1 spac=0.1
Y.MESH loc=2.1 spac=0.2
#
#
REGION     num=1  material=AlGaAs x.min=-2 x.max=11.0 \
 y.min=0.0 y.max=0.75 x.composition=0.5
#
REGION     num=2  material=GaAs x.min=-2 x.max=11 \
 y.min=0.75 y.max=0.85
#
REGION     num=3  material=AlGaAs x.min=-2 x.max=11 \
 y.min=0.85 y.max=2.1 x.composition=0.5
#
ELECTRODE       num=1  name=anode     x.min=3.0 x.max=6.0 y.min=0.0 y.max=0.0
ELECTRODE       num=2  name=cathode   bottom
#
DOPING     uniform reg=1  p.type conc=1.e18
DOPING     uniform reg=2  n.type conc=4.e15
DOPING     uniform reg=3  n.type conc=4.e17
#
MATERIAL material=GaAs   taun0=1.e-9 taup0=1.e-9 copt=1.5e-10 
MATERIAL material=AlGaAs taun0=1.e-9 taup0=1.e-9 copt=1.5e-10 align=0.6
#
MODEL  
MODEL material=GaAs     fldmob srh optr print
MODEL material=AlGaAs   fldmob srh optr
#
SOLVE init
OUTPUT con.band val.band
SAVE outf= $'name'_0.str
#

METHOD newton autonr trap  maxtrap=6 climit=1e-6
 
SOLVE vanode=0.1
SOLVE vanode=0.2
SOLVE vanode=0.4 vstep=0.4 name=anode vfinal=1.2
#
LX.M  n=1  x=0
LX.M  n=37 x=9
#
LY.M  n=1  y=0.5
LY.M  n=33 y=1.1
#
MODELS material=GaAs fldmob  srh optr fermi print \
 laser gainmod=1 las_maxch=100. \
 las_xmin=0.1 las_xmax=8.9 las_ymin=0.5 las_ymax=1.1 \
 photon_energy=1.43 las_nx=37 las_ny=33 spec.name=$'name'sp \
 lmodes las_einit=1.415 las_efinal=1.47 cavity_length=100
#
LOG outf= $'name'.log
#
SOLVE vanode=1.3 vstep=0.05 name=anode vfinal=2.0
#
CONTACT num=1 current
#
SOLVE prev istep=0.0005 ifinal=0.004 electr=1 
SAVE outfile= $'name'_1.str
#

victoryvisual $'name'_0.str $'name'.log:$'name'.set $'name'_1.str:$'name'_1.set

QUIT

