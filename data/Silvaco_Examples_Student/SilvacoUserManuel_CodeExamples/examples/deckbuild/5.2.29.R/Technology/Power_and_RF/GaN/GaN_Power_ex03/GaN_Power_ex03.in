# (c) Silvaco Inc., 2024

# Large Signal Output Power Analysis

# Required Modules
#  Victory Device
#   -2D Simulator

# Example Name
SET name="GaN_Power_ex03"

GO victorydevice simflags="-P 4"

#### STRUCTURE SPECIFICATION ####
MESH width=500 ^diag.flip

X.MESH location=-2.5  spacing=0.5
X.MESH location=-1.1  spacing=0.1
X.MESH location=-1    spacing=0.1
X.MESH location=-0.5  spacing=0.05
X.MESH location=-0.4  spacing=0.05
X.MESH location=-0.1  spacing=0.025
X.MESH location=0     spacing=0.02
X.MESH location=0.25  spacing=0.01
X.MESH location=0.35  spacing=0.025
X.MESH location=0.65  spacing=0.05
X.MESH location=0.75  spacing=0.05
X.MESH location=1.5   spacing=0.1
X.MESH location=3.2   spacing=0.1
X.MESH location=3.3   spacing=0.1
X.MESH location=4.5   spacing=0.5

Y.MESH location=-1.4   spacing=0.1
Y.MESH location=-1.3   spacing=0.3
Y.MESH location=-0.6   spacing=0.05
Y.MESH location=-0.5   spacing=0.05
Y.MESH location=-0.3   spacing=0.05
Y.MESH location=-0.1   spacing=0.025
Y.MESH location=0      spacing=0.001
Y.MESH location=0.011  spacing=0.003
Y.MESH location=0.023  spacing=0.001
Y.MESH location=0.4    spacing=0.05
Y.MESH location=1.923  spacing=0.25

REGION number=1 material=oxide y.maximum=0
REGION number=2 material=GaN y.minimum=0.023 polarization substrate
REGION number=3 material=AlGaN y.minimum=0 y.maximum=0.023 \
       x.comp=0.28 polarization calc.strain
REGION number=4 material=air x.minimum=-1.0 x.maximum=-0.5 y.maximum=-0.1
REGION number=4 material=air x.minimum=-0.5 x.maximum=-0.1 y.minimum=-0.5 \
   y.maximum=-0.1
REGION number=4 material=air x.minimum=0.35 x.maximum=0.75 y.minimum=-0.5 \
   y.maximum=-0.1
REGION number=4 material=air x.minimum=0.75 x.maximum=3.2 y.maximum=-0.1

ELECTRODE name=source x.maximum=-1.1 y.maximum=0.023
ELECTRODE name=gate x.minimum=0 x.maximum=0.25 y.minimum=-0.6 y.maximum=0
ELECTRODE name=gate x.minimum=-0.4 x.maximum=0.65 y.minimum=-1.3 y.maximum=-0.6
ELECTRODE name=drain x.minimum=3.3 y.maximum=0.023

INTERFACE s.s thermionic

DOPING  n.type gaussian peak=0.023 concentration=6e19 \
   characteristic=0.01 lat.characteristic=0.01 x.maximum=-0.8
DOPING  n.type gaussian peak=0.023 concentration=6e19 \
   characteristic=0.01 lat.characteristic=0.01 x.minimum=3.0

DOPING trap acceptor e.level=1.0 concentration=1e18 gaussian peak=1 \
   characteristic=0.455 degeneracy.factor=1 sign=1e-15 sigp=1e-15

#### MATERIAL/PHYSICAL MODELS ####  
MODELS srh print
MOBILITY fmct.n gansat.n mup0=50

CONTACT name=gate workfunction=5.15 resistance=50 ohms
CONTACT name=drain resistance=50 ohms

#### NUMERICAL METHODS ####
METHOD newton
OUTPUT con.band val.band charge polar.charge qss traps

# Sinusoid WAVEFORM setup.
set index=0
set ampv=0.18

loop steps=9
 set index=$'index'+1
 WAVEFORM number=$'index' sinusoid frequency=1e10 amplitude=$'ampv' \
    elec.name=gate num.samples=32 time.sample log.sample ^continue \
    periods.num.sample=4
 set ampv=$'ampv'*1.8
l.end

#### SOLUTION SOLVING #### 
SOLVE init
SOLVE previous
SAVE outfile=$'name'_VD_init.str

# Set Vdq=30V and Vgq=-5.5V
SOLVE vdrain=0.01 prev
SOLVE vdrain=0.1
SOLVE vdrain=0.25 vstep=0.25 vfinal=5 name=drain
SOLVE vstep=2.5 vfinal=30 name=drain
SOLVE vgate=-0.25 vstep=-0.25 vfinal=-5.5 name=gate
SAVE outfile=$'name'_VD_DCbias.str

# Transient analysis for each of the 9 WAVEFORMs.
SET index=0
SET logfilelist=" "
#### NUMERICAL METHODS ####
METHOD itlimit=50 tol.time=1e9 dt.max=1e-10/32

LOOP steps=9
 SET index=$'index'+1
 SET logfilelist="$'logfilelist' $'name'_VD_RF_wave$'index'.log"
 LOAD infile=$'name'_VD_DCbias.str
 SOLVE prev

# save / log current voltage data on electrodes
# from this point onwards to file 
 LOG outfile=$'name'_VD_RF_wave$'index'.log
 SOLVE wave$'index' dt=1e-10/32 tfinal=4e-10 previous
 LOG off
L.END

# convert time domain data into frequency domain and calculate the powers.
FOURIER infile.list="$'logfilelist'" outfile=$'name'_VD_RF_FFT.log \
   inport=gate outport=drain num.harmonic=3 fundamental=1e10 \
   complex.values num.samples=32 elec.bias period=4

victoryvisual \
$'name'_VD_init.str:$'name'_ElecConc_Init.set \
$'name'_VD_init.str:$'name'_IonizAccTrapConc_Init.set \
$'name'_VD_RF_FFT.log:$'name'_PinPout.set \
$'name'_VD_RF_wave1.log+$'name'_VD_RF_wave2.log+\
$'name'_VD_RF_wave3.log+$'name'_VD_RF_wave4.log+\
$'name'_VD_RF_wave5.log+$'name'_VD_RF_wave6.log+\
$'name'_VD_RF_wave7.log+$'name'_VD_RF_wave8.log+\
$'name'_VD_RF_wave9.log

QUIT
