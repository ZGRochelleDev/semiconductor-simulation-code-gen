# (c) Silvaco Inc., 2024
# 
# Temperature Properties of Amorphous In-Ga-Zn-O Thin-Film Transistors with a new Mobility Model
#
# IGZO.TOKYO mobility model with electron conconcentration and temperature.


####### VARIABLE DEFINITION #######
#
SET name=TFT_ex14
# temperature swept in DBInternal
SET set_t=300

####### DEVICE SIMULATION #######
GO victorydevice simflags="-P 4"

#### STRUCTURE SPECIFICATION ####

## Structure parameters
# IGZO thickness
SET igzo_th=0.04
# gate lenght 40 um, width 10um
SET lsz=40
SET wsz=10

MESH outfile=$'name'_0.str width=$wsz

X.MESH location=0 spacing=1
X.MESH location=1 spacing=1
X.MESH location=4.6 spacing=0.1
X.MESH location=5 spacing=0.1
X.MESH location=5.4 spacing=0.1
X.MESH location=6 spacing=0.1
X.MESH location=0.5*$'lsz'+5 spacing=1
X.MESH location=$'lsz'+4 spacing=0.1
X.MESH location=$'lsz'+4.6 spacing=0.1
X.MESH location=$'lsz'+5 spacing=0.1
X.MESH location=$'lsz'+5.4 spacing=0.1
X.MESH location=$'lsz'+9 spacing=1
X.MESH location=$'lsz'+10 spacing=1

Y.MESH location=-0.4 spacing=0.1
Y.MESH location=-0.2 spacing=0.02
Y.MESH location=-0.1 spacing=0.02
Y.MESH location=-0.02 spacing=0.005
Y.MESH location=0 spacing=0.002
Y.MESH location=0.5*$'igzo_th' spacing=0.005
Y.MESH location=$'igzo_th' spacing=0.002
Y.MESH location=0.02+$'igzo_th' spacing=0.005
Y.MESH location=0.04+$'igzo_th' spacing=0.01
Y.MESH location=0.05+$'igzo_th' spacing=0.02
Y.MESH location=0.06+$'igzo_th' spacing=0.05
Y.MESH location=0.2+$'igzo_th' spacing=0.1
#
# The device is composed of a 40 nm layer of IGZO deposited
# Gate insulater is composed of 200 nm SiO2.
# Protection is 200nm SiO2 and Passivation is 
#
REGION number=1 material=igzo y.minimum=0 y.maximum=$'igzo_th'
REGION number=2 material=sio2 y.minimum=$'igzo_th' y.maximum=0.2+$'igzo_th'
REGION number=3 material=sio2 x.minimum=5 x.maximum=$'lsz'+5 \
 y.minimum=-0.2 y.maximum=0
REGION number=4 material=sin y.minimum=-0.4 y.maximum=-0.2
REGION number=5 material=sin x.minimum=0 x.maximum=5 y.minimum=-0.2 y.maximum=0
REGION number=6 material=sin x.minimum=$'lsz'+5 x.maximum=$'lsz'+10 \
 y.minimum=-0.2 y.maximum=0

# electrodes
ELECTRODE number=1 name=gate y.minimum=0.2+$'igzo_th' y.maximum=0.2+$'igzo_th'
ELECTRODE number=2 name=source y.minimum=0 y.maximum=0 x.minimum=0 x.maximum=2
ELECTRODE number=3 name=drain y.minimum=0 y.maximum=0 \
 x.minimum=$'lsz'+8 x.maximum=$lsz+10

# doping
# channel
DOPING region=1 uniform concentration=8e15 n.type
# SD
DOPING region=1 gaussian x.right=5 concentration=1e20 characteristic=0.3 n.type
DOPING region=1 gaussian x.left=$'lsz'+5 concentration=1e20 \
 characteristic=0.3 n.type

#### MATERIAL/PHYSICAL MODELS ####
#
# IGZO parameters
#
MATERIAL region=1 semiconductor \
 m.dsn=0.34 m.dsp=1.5 eg300=3.1 affinity=4.2 permittivity=13

## IGZO.TOKYO parameters
# IGZO.TOKYO mu0 of electron
SET mue0=14
# IGZO.TOKYO Tgamma of electron
SET tgm=178.33
# IGZO.TOKYO gamma0 of electron
SET gm0=-0.3088
# IGZO.TOKYO critical carrier conc. of electron
SET necr=10e19

# IGZO.TOKYO mobility model
MOBILITY igzo.tokyo mun=$mue0 mup=0.01 tmun=0 tmup=0 \
 igzo.gamma0=$gm0 igzo.tgamma=$tgm igzo.ncrit=$necr

# cvd-sio2
MATERIAL material=oxide permittivity=4.2

## DOS based on the reference paper
# acceptor-like tail state
SET ntac=1e19
SET wtac=0.008
# acceptor-like Gaussian state
SET ngac=1.5e17
SET egac=0.0
SET wgac=0.64
# donor-like tail state
SET ntdo=3.0e19
SET wtdo=0.12
# donor-like Gaussian state
SET ngdo=1.3e17
SET egdo=2.48
SET wgdo=0.24
#
# Defects
#
DEFECTS continuous \
 nta=$ntac ntd=$ntdo wta=$wtac wtd=$wtdo \
 nga=$ngac ngd=$ngdo ega=$egac egd=$egdo wga=$wgac wgd=$wgdo \
 sigtae=1e-17 sigtah=1e-15 sigtde=1e-15 sigtdh=1e-17 \
 siggae=2e-16 siggah=2e-15 siggde=2e-15 siggdh=2e-16 \
 tfile=$'name'_igzo_ext-std1_dos.dat \
 numa=200 numd=200

#
# contacts 
# Ti 4.3
# Mo 4.53
#
CONTACT workfunction=4.53 name=gate
CONTACT workfunction=4.3 name=source
CONTACT workfunction=4.3 name=drain

# models
MODELS srh fermidirac print temp=$set_t

#### NUMERICAL METHODS ####
# outputs
OUTPUT band.para band.temp con.band val.band e.mobility h.mobility

# probe
PROBE name="ne_1n" n.conc x=$'lsz'*0.5+5 y=$'igzo_th'-0.001
PROBE name="mob_1n" n.mob x=$'lsz'*0.5+5 y=$'igzo_th'-0.001
PROBE name="ne_2n" n.conc x=$'lsz'*0.5+5 y=$'igzo_th'-0.002
PROBE name="mob_2n" n.mob x=$'lsz'*0.5+5 y=$'igzo_th'-0.002
PROBE name="ne_5n" n.conc x=$'lsz'*0.5+5 y=$'igzo_th'-0.005
PROBE name="mob_5n" n.mob x=$'lsz'*0.5+5 y=$'igzo_th'-0.005
PROBE name="ne_10n" n.conc x=$'lsz'*0.5+5 y=$'igzo_th'-0.01
PROBE name="mob_10n" n.mob x=$'lsz'*0.5+5 y=$'igzo_th'-0.01
PROBE name="ne_20n" n.conc x=$'lsz'*0.5+5 y=$'igzo_th'-0.02
PROBE name="mob_20n" n.mob x=$'lsz'*0.5+5 y=$'igzo_th'-0.02

#### SOLUTION SOLVING ####
#
# Solve
#
SOLVE initial
SAVE outfile=$'name'_init.str

# method
METHOD newton climit=1e-5 maxtraps=10 itlimit=40 ir.tol=5e-16 ix.tol=2e-6 \
 carriers=1 electrons

# id-vg at Vd=0.1
SOLVE previous
SOLVE vdrain=0.01
SOLVE vdrain=0.1

SOLVE vgate=0.1
SOLVE vgate=1
SOLVE vgate=2 vstep=2 vfinal=20 name=gate
#SAVE outfile=$'name'_vg20vd01.str

LOG outfile=$'name'_vd01_$'set_t'.log
SOLVE vgate=20 vstep=-0.25 vfinal=-5 name=gate
#SAVE outfile=$'name'_vgm10vd01.str

#EXTRACT name="idvg01" curve( v."gate", abs(i."drain") ) outfile="idvg01_$'set_t'.dat"
#EXTRACT name="mob01" deriv( v."gate", abs(i."drain")/($wsz*1.73e-8*(4.2/3.9)*0.1/$lsz) ) outfile="mob-lin_$'set_t'.dat"

## set id at vth
SET i_vt=1.e-10
EXTRACT name="vt_01" x.val from curve( v."gate", abs(i."drain") ) where y.val=$i_vt
EXTRACT name="ss_01" y.val from deriv( log10( abs(i."drain") ), v."gate" ) where x.val=log10($i_vt)
EXTRACT name="ion_01" y.val from curve( v."gate", abs(i."drain") ) where x.val=20
EXTRACT name="mul_01" y.val from deriv( v."gate", abs(i."drain")/($wsz*1.73e-8*0.1/$lsz) ) where x.val=20

LOG off

QUIT
