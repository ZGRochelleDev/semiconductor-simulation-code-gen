# (c) Silvaco Inc., 2024

# Pasveer Hopping Mobility Model and Gaussian DOS

# Required Modules
#  Victory Device
#   -2D Simulator
#   -2D Organic

###### VARIABLES ######
# Example Name
SET name="LED_OLED_ex07"

###########################################################################
# The device van Mensfoort and Coehoorn model is
#    * 100 nm (0.1 um) long
#    * Nt = 8.51e26 m^-3 = 8.51e20 cm^-3
#    * mu0 = 1e-10 m^2/Vs = 1e-6 cm^2/Vs
#    * kappa = 3
#    * sighat = 6
#    * n1 = 4.25e26 m^-3 = 4.25e20 cm^-3: carrier concentration at the CONTACT 
#      (NB, this is basically Nt/2 so the work function is in the
#       middle of the LUMO level, the nominal conduction band.)
# I add
#    * eg = 3
# This gives NC = 5.59e30
###########################################################################
# Fundamental constants
ASSIGN name=k n.value=1.3806488e-23
ASSIGN name=q n.value=1.602176565e-19

###########################################################################
# sighat is the width of the Gaussian normalised to kT
# normc is the carrier concentration normalised to the density of the Gaussian.
# normf is the normalised electric field
# tl is the lattice temperature
SET sighat=6
SET tl=300

###########################################################################
# Concentration of the Gaussian
ASSIGN name=Nt n.value=8.51e20
# average separation between sites
ASSIGN name=a n.value=1e7/pow(${Nt},0.3333333333)
# The width of the Gaussian is sig = sighat*k*T (in eV)
ASSIGN name=sig n.value=${sighat}*${k}*${tl}/${q}

###########################################################################
ASSIGN name=eg n.value=3
ASSIGN name=chi n.value=1
ASSIGN name=kappa n.value=3

###########################################################################
ASSIGN name=mun0 n.value=1e-6
ASSIGN name=mup0 n.value=1e-6

###########################################################################
# The CONTACT work function is designed to give n=4.25e20
# This is basically half of Nt so the Fermi level is the middle
# of the Gaussian band
ASSIGN name=wf n.value=${chi}
###########################################################################
SET nstep=10

###### DEVICE SIMULATION #####
GO victorydevice simflags="-P 2"

#### STRUCTURE SPECIFICATION ####
# By making the width 1e12 um the xz area is 1cm^2
# Therefore current and current density have the same value.
MESH width=1e12
X.MESH location=0   spacing=0.25
X.MESH location=1   spacing=0.25

Y.MESH location=0   spacing=0.001
Y.MESH location=0.1 spacing=0.005

REGION number=1 Material=organic x.minimum=0  x.maximum=1 y.minimum=0 y.maximum=0.1

ELECTRODE name=anode   top
ELECTRODE name=cathode bottom

#### MATERIAL/PHYSICAL MODELS ####
MATERIAL number=1 ntc.gauss=$Nt sigc.gauss=$sig eg300=${eg} \
         permittivity=${kappa} affinity=${chi}

CONTACT name=anode   workf=${wf}
CONTACT name=cathode workf=${wf}

###########################################################################
# We can SET a.pasv/sig.pasv independently, but if we have a gaussian
# band structure the default values are from that.
MOBILITY mup=${mup0} mun=${mun0} pasveer.n ccutoff.n=0.5 fcutoff.n=2
###########################################################################
MODELS print

#### NUMERICAL METHODS ####
METHOD itlimit=250 cx.tol=1e-10 

#### SOLUTION SOLVING ####
SOLVE initial
SOLVE vanode=0
SAVE outfile=${name}_pasveer_Va0.str
SOLVE vanode=0.01

LOG outfile=${name}_pasveer.log
SET v1=0.01
SET v2=0.5
SET s1=exp(log($v2/$v1) / $nstep)
SOLVE vstep=$s1 vmult vfinal=$v2 name=anode
SAVE outfile=${name}_pasveer_Va${v2}.str
SET v3=2
SET s2=exp(log($v3/$v2) / $nstep)
SOLVE vstep=$s2 vmult vfinal=$v3 name=anode
SAVE outfile=${name}_pasveer_Va${v3}.str
SET v4=8
SET s3=exp(log($v4/$v3) / $nstep)
SOLVE vstep=$s3 vmult vfinal=$v4 name=anode
SAVE outfile=${name}_pasveer_Va${v4}.str
SET v5=20
SET s4=exp(log($v5/$v4) / $nstep)
SOLVE vstep=$s4 vmult vfinal=$v5 name=anode

###########################################################################
# Repeat the previous simulation, but with constant MOBILITY
###########################################################################
###### DEVICE SIMULATION #####
GO victoryd simflags="-P 2"

#### STRUCTURE SPECIFICATION ####
# By making the width 1e8 um the xz area is 1cm^2
# Therefore current and current density have the same value.
MESH width=1e12
X.MESH location=0   spacing=0.25
X.MESH location=1   spacing=0.25

Y.MESH location=0   spacing=0.001
Y.MESH location=0.1 spacing=0.005

REGION number=1 Material=organic x.minimum=0  x.maximum=1 y.minimum=0 y.maximum=0.1

ELECTRODE name=anode top
ELECTRODE name=cathode bottom

#### MATERIAL/PHYSICAL MODELS ####
MATERIAL number=1 ntc.gauss=$Nt sigc.gauss=$sig eg300=${eg} \
         permittivity=${kappa} affinity=${chi}

CONTACT name=anode workf=${wf}
CONTACT name=cathode workf=${wf}

###########################################################################
# We can SET a.pasv/sig.pasv independently, but if we have a gaussian
# band structure the default values are from that.
MOBILITY mup=${mup0} mun=${mun0}
###########################################################################
MODELS print

#### NUMERICAL METHODS ####
METHOD itlimit=250 cx.tol=1e-10 

#### SOLUTION SOLVING ####
SOLVE init
SOLVE vanode=0
SAVE outfile=${name}_constant_Va0.str
SOLVE vanode=0.01

LOG outfile=${name}_constant.log
SET v1=0.01
SET v2=0.5
SET s1=exp(log($v2/$v1) / $nstep)
SOLVE vstep=$s1 vmult vfinal=$v2 name=anode
SAVE outfile=${name}_constant_Va${v2}.str
SET v3=2
SET s2=exp(log($v3/$v2) / $nstep)
SOLVE vstep=$s2 vmult vfinal=$v3 name=anode
SAVE outfile=${name}_constant_Va${v3}.str
SET v4=8
SET s3=exp(log($v4/$v3) / $nstep)
SOLVE vstep=$s3 vmult vfinal=$v4 name=anode
SAVE outfile=${name}_constant_Va${v4}.str
SET v5=20
SET s4=exp(log($v5/$v4) / $nstep)
SOLVE vstep=$s4 vmult vfinal=$v5 name=anode

##### EXTRACT #####
go victoryextract
LOAD in="$'name'_pasveer_Va0.str"
EXTRACT.PROFILE fields="Electron Conc" regions=* x.val=0.5 csvfile="$'name'_pasveer_cut_Va0"
LOAD in="$'name'_pasveer_Va2.str"
EXTRACT.PROFILE fields="Electron Conc" regions=* x.val=0.5 csvfile="$'name'_pasveer_cut_Va2"
LOAD in="$'name'_pasveer_Va8.str"
EXTRACT.PROFILE fields="Electron Conc" regions=* x.val=0.5 csvfile="$'name'_pasveer_cut_Va8"


###### VISUALIZATION ######
# Plot the Electron Concentrations
victoryvisual ${name}_pasveer_Va0.str:${name}_pasveer_Va.set ${name}_pasveer_Va${v2}.str:${name}_pasveer_Va.set ${name}_pasveer_Va${v3}.str:${name}_pasveer_Va.set ${name}_pasveer_Va${v4}.str:${name}_pasveer_Va.set

victoryvisual '(${name}_pasveer_cut_Va0.csv+${name}_pasveer_cut_Va2.csv+${name}_pasveer_cut_Va8.csv)':${name}_pasveer_cut.set
# Plot IV
victoryvisual '(${name}_pasveer.log+${name}_constant.log)':${name}_iv.set

QUIT
