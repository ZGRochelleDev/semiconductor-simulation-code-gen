# (c) Silvaco Inc., 2024

# Quantum Tunnelling in a Silicon Diode 

# Required Modules
#  Victory Device
#   -2D Simulator
#   -2D Quantum

# Example Name
Set name=“Bip_Dio_ex06” 

####### DEVICE SIMULATION ####### 
GO victoryd simflags="-P 4"

#### STRUCTURE SPECIFICATION ####
mesh ^diag.flip

X.MESH location=0.0 spacing=0.1
X.MESH location=1.0 spacing=0.1

Y.MESH location=0 spacing=0.1
Y.MESH location=0.9 spacing=0.05
Y.MESH location=0.95 spacing=0.001
Y.MESH location=1.05 spacing=0.001
Y.MESH location=1.1 spacing=0.05
Y.MESH location=2 spacing=0.1

REGION number=1 material=silicon

ELECTRODE name=anode top
ELECTRODE name=cathode bottom 

QTX.MESH location=0.0 spacing=0.1
QTX.MESH location=1.0 spacing=0.1
QTY.MESH location=0.95 spacing=0.00076
QTY.MESH location=1.05 spacing=0.00076

DOPING uniform p.type conc=2e20 y.max=1.0
DOPING uniform n.type conc=2e20 y.min=1.0

#### MATERIAL/PHYSICAL MODELS ####
MODELS temperature=200 srh fermi ni.fermi print bbt.nonlocal bbt.nlderivs 

OUTPUT band.temp traps u.srh

#### NUMERICAL METHODS ####
METHOD climit=1.0 dvmax=0.005

#### SOLUTION SOLVING #### 
SOLVE init

SAVE outfile=$'name'_Init.str

LOG outf=$'name'_reverse.log

SOLVE name=anode vanode=-0.0 vstep=-0.01 vfinal=-0.05
SOLVE name=anode vanode=-0.05 vstep=-0.05 vfinal=-0.2
SOLVE name=anode vanode=-0.25 vstep=-0.05 vfinal=-0.5

log off

SOLVE init

LOG outf=$'name'_forward.log

SOLVE name=anode vanode=0.0 vstep=0.005 vfinal=0.2
SOLVE name=anode vanode=0.205 vstep=0.005 vfinal=0.35
SOLVE name=anode vanode=0.375 vstep=0.025 vfinal=1.2

LOG off

victoryvisual \
$'name'_reverse.log:$'name'_IV.set+\
$'name'_forward.log:$'name'_IV.set

QUIT
