# (c) Silvaco Inc., 2024

# The "Kink" Effect in Partially Depleted SOI MOSFETs

# Required Modules
#  Victory Device
#   -2D Simulator

# Example Name
Set name=“SOI_ex03” 

GO victoryd simflags="-P 4"
#
# 0.2um of silicon on 0.4um oxide substrate 
# 
#### STRUCTURE SPECIFICATION ####
# Mesh Definition
Mesh space.mult=1.0
# 
X.MESH location=0.00 spacing=0.50
X.MESH location=1.15 spacing=0.02
X.MESH location=1.5 spacing=0.1
X.MESH location=1.85 spacing=0.02
X.MESH location=2 spacing=0.02
X.MESH location=3 spacing=0.5
#
Y.MESH location=-0.017 spacing=0.02
Y.MESH location=0.00 spacing=0.005
Y.MESH location=0.15 spacing=0.02
Y.MESH location=0.3 spacing=0.01
Y.MESH location=0.7 spacing=0.25
#
## Regions and electrodes
REGION num=1 y.max=0 oxide 
REGION num=2 y.min=0 y.max=0.3 silicon
REGION num=3 y.min=0.3 oxide
#
ELECTRODE name=gate x.min=1 x.max=2 y.min=-0.017 y.max=-0.017
ELECTRODE name=source x.max=0.5 y.min=0 y.max=0
ELECTRODE name=drain x.min=2.5 y.min=0 y.max=0
ELECTRODE substrate
#
#Device doping
#
DOPING uniform conc=1e17 p.type reg=2
DOPING gauss n.type conc=1e20 char=0.2 lat.char=0.05 reg=2 x.r=1.0 
DOPING gauss n.type conc=1e20 char=0.2 lat.char=0.05 reg=2 x.l=2.0 
SAVE outf=$'name'_VD.str

#
#### MATERIAL/PHYSICAL MODELS #### 
INTERFACE qf=3e10 y.max=0.1
INTERFACE qf=1e11 y.min=0.1
CONTACT name=gate n.poly
MODELS cvt srh bgn print 
IMPACT selb

#### SOLUTION SOLVING #### 
SOLVE init
METHOD direct climit=1.0e-4
SOLVE prev 
SOLVE vdrain=0.05
SOLVE vdrain=0.1

# ramp gate voltage to 1, 2 and 3V
SOLVE vgate=0.2 vstep=0.2 name=gate vfinal=1
SOLVE outf=$'name'_1.str
SOLVE vstep=0.2 name=gate vfinal=2
SOLVE outf=$'name'_2.str
SOLVE vstep=0.2 name=gate vfinal=3
SAVE outf=$'name'_3.str
#
# Now do ID/VDS characteristic
LOAD infile=$'name'_1.str master
SOLVE prev
LOG outf=$'name'_1.log
SOLVE vfinal=3.3 vstep=0.1 name=drain
LOG outf=tmp
#
LOAD infile=$'name'_2.str master
SOLVE prev
LOG outf=$'name'_2.log
SOLVE vfinal=3.3 vstep=0.1 name=drain
LOG outf=tmp
#
LOAD infile=$'name'_3.str master
SOLVE prev
LOG outf=$'name'_3.log
SOLVE vfinal=3.3 vstep=0.1 name=drain

victoryvisual $'name'_VD.str \
$'name'_1.log+$'name'_2.log+$'name'_3.log

QUIT
