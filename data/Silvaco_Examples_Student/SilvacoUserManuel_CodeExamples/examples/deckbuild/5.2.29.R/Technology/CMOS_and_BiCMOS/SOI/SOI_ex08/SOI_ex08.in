# (c) Silvaco Inc., 2024

# Use of Body Contact to Suppress Kink Effect

# Required Modules
#  Victory Process 
#   -3D structure editor
#  Victory Mesh
#   -3D Meshing
#  Victory Device
#   -3D Simulator
#   -3D Giga

# Example Name
Set name=“SOI_ex08” 

# Definition of an SOI structure WITHOUT a body contact
####### PROCESS SIMULATION ####### 
GO victoryprocess simflags="-P 4"

INIT material="oxide" GasHeight=1 Depth=0.4 from="0, 0" to ="3, 4"

SPECIFYMASKPoly MASKID=1 istransparent=false p="1, 0" p="2, 0" p="2, 4" p="1, 4"

SPECIFYMASKPoly MASKID=2 istransparent=false p="1, 0" p="2, 0" p="2, 4" p="1, 4"

SPECIFYMASKPoly MASKID=2 ADD istransparent=false p="0, 0" p="0.45, 0" p="0.45, 4" p="0, 4"

SPECIFYMASKPoly MASKID=2 ADD istransparent=false p="2.55, 0" p="3, 0" p="3, 4" p="2.55, 4"

LINE x location=0 spacing=0.2
LINE x location=1 spacing=0.025
LINE x location=1.5 spacing=0.1
LINE x location=2 spacing=0.025
LINE x location=3 spacing=0.2

LINE y loc=0 spac=0.25
LINE y loc=2 spac=0.1
LINE y loc=2.5 spac=0.2
LINE y loc=4 spac=0.25

LINE z location=-1 spacing=0.1
LINE z location=-0.267 spacing=0.05
LINE z location=-0.217 spacing=0.005
LINE z location=-0.2 spacing=0.0025
LINE z location=0 spacing=0.05
LINE z location=0.4 spacing=0.2

#
DEPOSIT silicon thick=0.2 boron=2e17 max

MASK MASKID=1 Reverse
DOPING silicon arsenic=1e20 min=0 max=0.2 
STRIP resist

DEPOSIT oxide thickness=0.017 max

ETCH oxide MASKID=1 reverse thickness=0.017 max

DEPOSIT aluminum thickness=0.05 min
ETCH aluminum MASKID=2 Reverse thickness=0.05 max

# SAVE simulation status for remeshing before electrical simulation
SAVE name=$'name'_NO_BC

####### STRUCTURE REMESHING ####### 
GO victorymesh simflags="-P 4"

# Load simulation status
LOAD in=$'name'_NO_BC

REMESH conformal

SAVE out=$'name'_VM_NO_BC.str

####### DEVICE SIMULATION #######

# This deck simulates an SOI device without a body contact,
# demonstrating the Kink effect in the IV curve.

GO victorydevice simflags="-P 4"

#### STRUCTURE SPECIFICATION ####

# Load the device structure
MESH inf=$'name'_VM_NO_BC.str prism delaunay hybridx

# Specify that the substrate is an ELECTRODE 
ELECTRODE name=source region=1
ELECTRODE name=gate region=2
ELECTRODE name=drain region=3
ELECTRODE name=substrate z.min=0.4 z.max=0.4

# For the gate, use a workfunction corresponding to n-type polysilicon
CONTACT name=gate n.poly

# Specify the physical MODELS 
MODELS cvt srh auger bgn
IMPACT selb p.min=1e7

# Set solver parameters

# ILUP PC.FILL_RATIO=3.0 changes the preconditioner for the linear solver
# CX.TOL=1.0e-6 tightens the tolerance on the carrier concentration solution
#
METHOD bicgst ilup pc.fill_ratio=3.0 cx.tol=1.0e-6

# Initialize the solution
SOLVE init
SOLVE prev

# Put a small initial bias on the drain
SOLVE vdrain=0.0001
SOLVE vdrain=0.005
SOLVE vdrain=0.010
SOLVE vdrain=0.025 vfinal=0.1 vstep=0.025 name=drain prev

# Ramp the gate bias until current starts to flow
LOG outf=$'name'_VGID_NO_BC.log
SOLVE vgate=0.1 vfinal=0.4 vstep=0.1 name=gate prev

# Now tweak the solver, and insist that it converge to relative tolerances
METHOD xnorm linear.tolerance=1e-15 linear.scaling=1 cx.tol=1.0e-6

# Continue ramping the gate bias
SOLVE vgate=0.5 vfinal=1  vstep=0.1 name=gate
SOLVE vgate=1.05 vfinal=1.175 vstep=0.025 name=gate
SOLVE vgate=1.2 vfinal=3  vstep=0.2 name=gate

# Bias the drain
LOG outf=$'name'_VDID_NO_BC.log
SOLVE vdrain=0.1025 vfinal=0.11 vstep=0.0025 name=drain
SOLVE vdrain=0.2 vfinal=4 vstep=0.1 name=drain

# OUTPUT the final conditions
OUTPUT band.param band.temp u.auger u.srh e.mobility h.mobility qfn qfp charge
SAVE outf=$'name'_VG3V_VD4V_NO_BC.str

LOG off

#----------------------------------------------------------------------#
#
# This deck simulates an SOI device without a body contact,
# demonstrating the Kink effect in the IV curve. The simulation also
# includes lattice heating, illustrating the temperature rise in the
# device.
#
#----------------------------------------------------------------------#

GO victorydevice simflags="-P 4"

# Load the device structure
MESH inf=$'name'_VM_NO_BC.str prism delaunay hybridx

# Specify ELECTRODE s
ELECTRODE name=source region=1
ELECTRODE name=gate region=2
ELECTRODE name=drain region=3
ELECTRODE name=substrate z.min=0.4 z.max=0.4

# For the gate, use a workfunction and thermal parameters 
# corresponding to n-type polysilicon
CONTACT name=gate n.poly
MATERIAL name=gate \
  tcon.power tc.const=1.5486 tc.npow=1.3333 \
  hc.comp hc.beta=1.85 hc.c1=255.0 hc.c300=711.0 hc.rho=2.33

# Define a thermal CONTACT at the substrate, held at 300 K
THERMCONTACT num=1 temp=300 x.min=0 x.max=3 y.min=0 y.max=4 z.min=0.4 z.max=0.4

# Specify the physical MODELS 
MODELS cvt srh auger bgn
IMPACT selb p.min=1e7

# Set solver parameters
#
# This device is only marginally stable. Because of this, in order
# to get the solution to converge, we must adjust some solver parameters. 
#
# ILUP PC.FILL_RATIO=3.0 changes the preconditioner for the linear solver
# CX.TOL=1.0e-6 tightens the tolerance on the carrier concentration solution
#
METHOD bicgst ilup pc.fill_ratio=3.0 cx.tol=1.0e-6

# Initialize the solution
SOLVE init
SOLVE prev

# Put a small initial bias on the drain
SOLVE vdrain=0.0001
SOLVE vdrain=0.005
SOLVE vdrain=0.010
SOLVE vdrain=0.025 vfinal=0.1 vstep=0.025 name=drain prev

# Ramp the gate bias until current starts to flow
LOG outf=$'name'_VGID_NO_BC_HEATING.log
SOLVE vgate=0.1 vfinal=0.4 vstep=0.1 name=gate prev

# Now tweak the solver, and insist that it converge to relative tolerances
METHOD xnorm linear.tolerance=1e-15 linear.scaling=1 cx.tol=1.0e-6

# Continue ramping the gate bias
SOLVE vgate=0.5 vfinal=1  vstep=0.1 name=gate
SOLVE vgate=1.05 vfinal=1.175 vstep=0.025 name=gate
SOLVE vgate=1.2 vfinal=3  vstep=0.2 name=gate
LOG off

# Activate lattice temperature simulation
MODELS lat.temp
SOLVE prev

# Bias the drain
LOG outf=$'name'_VDID_NO_BC_HEATING.log
SOLVE vdrain=0.1025 vfinal=0.11 vstep=0.0025 name=drain
SOLVE vdrain=0.2 vfinal=4 vstep=0.1 name=drain

# OUTPUT the final conditions
OUTPUT band.param band.temp u.auger u.srh e.mobility h.mobility qfn qfp charge
SAVE outf=$'name'_VG3V_VD4V_NO_BC_HEATING.str

LOG off


# Definition of an SOI structure WITH a body contact
####### PROCESS SIMULATION ####### 
GO victoryprocess simflags="-P 4"

INIT material="oxide" GasHeight=1 Depth=0.4 from="0, 0" to ="3, 4"

SPECIFYMASKPoly MASKID=1 istransparent=false p="1, 0" p="2, 0" p="2, 3.5" p="1, 3.5"

SPECIFYMASKPoly MASKID=2 istransparent=false p="1, 0" p="2, 0" p="2, 2.5" p="1, 2.5"

SPECIFYMASKPoly MASKID=2 ADD istransparent=false p="0, 0" p="0.45, 0" p="0.45, 1" p="0, 1"

SPECIFYMASKPoly MASKID=2 ADD istransparent=false p="2.55, 0" p="3, 0" p="3, 1" p="2.55, 1"

SPECIFYMASKPoly MASKID=2 ADD istransparent=false p="1, 3.5" p="2, 3.5" p="2, 4" p="1, 4"

SPECIFYMASKPoly MASKID=3 istransparent=false p="1, 3.5" p="2, 3.5" p="2, 4" p="1, 4"

SPECIFYMASKPoly MASKID=4 istransparent=false p="0, 0" p="1, 0" p="1, 2" p="0, 2"
SPECIFYMASKPoly MASKID=4 ADD istransparent=false p="2, 0" p="3, 0" p="3, 2" p="2, 2"

LINE x location=0 spacing=0.2
LINE x location=1 spacing=0.025
LINE x location=1.5 spacing=0.1
LINE x location=2 spacing=0.025
LINE x location=3 spacing=0.2

LINE y loc=0 spac=0.25
LINE y loc=2 spac=0.1
LINE y loc=2.5 spac=0.2
LINE y loc=4 spac=0.25

LINE z location=-1 spacing=0.1
LINE z location=-0.267 spacing=0.05
LINE z location=-0.217 spacing=0.005
LINE z location=-0.2 spacing=0.0025
LINE z location=0 spacing=0.05
LINE z location=0.4 spacing=0.2

DEPOSIT silicon thick=0.2 boron=2e17 max

MASK MASKID=3 
DOPING silicon boron=1e20 min=0 max=0.2 
STRIP resist

MASK MASKID=4 
DOPING silicon arsenic=1e20 min=0 max=0.2
STRIP resist

DEPOSIT oxide thickness=0.017 max

ETCH oxide MASKID=1 reverse thickness=0.017 max

DEPOSIT aluminum thickness=0.05 min
ETCH aluminum MASKID=2 Reverse thickness=0.05 max

# SAVE simulation status for remeshing before electrical simulation
SAVE name=$'name'_BC

####### STRUCTURE REMESHING ####### 
GO victorymesh simflags="-P 4"

# Load simulation status
LOAD in=$'name'_BC

REMESH conformal

SAVE out=$'name'_VM_BC.str


#----------------------------------------------------------------------#
#
# This deck simulates an SOI device with a body contact,
# demonstrating an IV curve without a kink.
#
#----------------------------------------------------------------------#

GO victorydevice simflags="-P 4"

# Load the device structure
MESH inf=$'name'_VM_BC.str prism delaunay hybridx 

# Specify that the substrate is an ELECTRODE 
ELECTRODE name=source region=1
ELECTRODE name=gate region=2
ELECTRODE name=drain region=3
ELECTRODE name=substrate z.min=0.4 z.max=0.4
ELECTRODE name=bulk region=4

# For the gate, use a workfunction corresponding to n-type polysilicon
CONTACT name=gate n.poly

# Specify the physical MODELS 
MODELS cvt srh auger bgn
IMPACT selb 

# Set solver parameters
#
# ILUP PC.FILL_RATIO=3.0 changes the preconditioner for the linear solver
# CX.TOL=1.0e-6 tightens the tolerance on the carrier concentration solution
#
METHOD bicgst ilup pc.fill_ratio=3.0 cx.tol=1.0e-6

# Initialize the solution
SOLVE init
SOLVE prev

# Put a small initial bias on the drain
SOLVE vdrain=0.0001
SOLVE vdrain=0.005
SOLVE vdrain=0.010
SOLVE vdrain=0.025 vfinal=0.1 vstep=0.025 name=drain prev

# Ramp the gate bias until current starts to flow
LOG outf=$'name'_VGID_BC.log
SOLVE vgate=0.1 vfinal=0.4 vstep=0.1 name=gate prev

# Now tweak the solver, and insist that it converge to relative tolerances
METHOD xnorm linear.tolerance=1e-15 linear.scaling=1 cx.tol=1.0e-6

# Continue ramping the gate bias
SOLVE vgate=0.5 vfinal=1  vstep=0.1 name=gate
SOLVE vgate=1.05 vfinal=1.175 vstep=0.025 name=gate
SOLVE vgate=1.2 vfinal=3  vstep=0.2 name=gate

# Bias the drain
LOG outf=$'name'_VDID_BC.log
SOLVE vdrain=0.1025 vfinal=0.11 vstep=0.0025 name=drain
SOLVE vdrain=0.2 vfinal=4 vstep=0.1 name=drain

# OUTPUT the final conditions
OUTPUT band.param band.temp u.auger u.srh e.mobility h.mobility qfn qfp charge
SAVE outf=$'name'_VG3V_VD4V_BC.str

LOG off

#----------------------------------------------------------------------#
#
# This deck simulates an SOI device with a body contact,
# demonstrating an IV curve without a kink. The simulation also
# includes lattice heating, illustrating the temperature rise in the
# device.
#
#----------------------------------------------------------------------#

GO victorydevice simflags="-P 4"

# Load the device structure
MESH inf=$'name'_VM_BC.str prism delaunay hybridx

# Specify ELECTRODE s
ELECTRODE name=source region=1
ELECTRODE name=gate region=2
ELECTRODE name=drain region=3
ELECTRODE name=substrate z.min=0.4 z.max=0.4
ELECTRODE name=bulk region=4


# For the gate, use a workfunction and thermal parameters 
# corresponding to n-type polysilicon
CONTACT name=gate n.poly
MATERIAL name=gate \
  tcon.power tc.const=1.5486 tc.npow=1.3333 \
  hc.comp hc.beta=1.85 hc.c1=255.0 hc.c300=711.0 hc.rho=2.33

# Define a thermal CONTACT at the substrate, held at 300 K
THERMCONTACT num=1 temp=300 x.min=0 x.max=3 y.min=0 y.max=4 z.min=0.4 z.max=0.4

# Specify the physical MODELS 
MODELS cvt srh auger bgn
IMPACT selb 

# Set solver parameters
#
# ILUP PC.FILL_RATIO=3.0 changes the preconditioner for the linear solver
# CX.TOL=1.0e-6 tightens the tolerance on the carrier concentration solution
#
METHOD bicgst ilup pc.fill_ratio=3.0 cx.tol=1.0e-6

# Initialize the solution
SOLVE init
SOLVE prev

# Put a small initial bias on the drain
SOLVE vdrain=0.0001
SOLVE vdrain=0.005
SOLVE vdrain=0.010
SOLVE vdrain=0.025 vfinal=0.1 vstep=0.025 name=drain prev

# Ramp the gate bias until current starts to flow
LOG outf=$'name'_VGID_BC_HEATING.log
SOLVE vgate=0.1 vfinal=0.4 vstep=0.1 name=gate prev

# Now tweak the solver, and insist that it converge to relative tolerances
METHOD xnorm linear.tolerance=1e-15 linear.scaling=1 cx.tol=1.0e-6

# Continue ramping the gate bias
SOLVE vgate=0.5 vfinal=1  vstep=0.1 name=gate prev
SOLVE vgate=1.05 vfinal=1.175 vstep=0.025 name=gate
SOLVE vgate=1.2 vfinal=3  vstep=0.2 name=gate
LOG off

# Activate lattice temperature simulation
MODELS lat.temp
SOLVE prev

# Bias the drain
LOG outf=$'name'_VDID_BC_HEATING.log
SOLVE vdrain=0.1025 vfinal=0.11 vstep=0.0025 name=drain
SOLVE vdrain=0.2 vfinal=4 vstep=0.1 name=drain

# OUTPUT the final conditions
OUTPUT band.param band.temp u.auger u.srh e.mobility h.mobility qfn qfp charge
SAVE outf=$'name'_VG3V_VD4V_BC_HEATING.str

LOG off


# Compare and contrast the IV curves for the four cases:
#
# 1) No body contact, heating not considered (SOI10_VDID_NO_BC.log)
# 2) No body contact, with lattice heating (SOI10_VDID_NO_BC_HEATING.log)
# 3) Body contact, heating not considered (SOI10_VDID_BC.log)
# 4) Body contact, with lattic heating  (SOI10_VDID_BC_HEATING.log)
#
extract init inf="$'name'_VDID_NO_BC.log"
extract name="IV" curve(abs(v."drain"),abs(i."drain")) outf="$'name'_NO_BC.dat"
  
extract init inf="$'name'_VDID_NO_BC_HEATING.log"
extract name="IV" curve(abs(v."drain"),abs(i."drain")) outf="$'name'_NO_BC_HEATING.dat"

extract init inf="$'name'_VDID_BC.log"
extract name="IV" curve(abs(v."drain"),abs(i."drain")) outf="$'name'_BC.dat"

extract init inf="$'name'_VDID_BC_HEATING.log"
extract name="IV" curve(abs(v."drain"),abs(i."drain")) outf="$'name'_BC_HEATING.dat"


victoryvisual \
$'name'_NO_BC.dat+$'name'_NO_BC_HEATING.dat+$'name'_BC.dat+$'name'_BC_HEATING.dat \
$'name'_VG3V_VD4V_NO_BC_HEATING.str:$'name'_VG3V_VD4V_NO_BC_HEATING.set \
$'name'_VG3V_VD4V_BC_HEATING.str:$'name'_VG3V_VD4V_BC_HEATING.set

QUIT
