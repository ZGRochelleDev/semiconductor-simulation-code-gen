# (c) Silvaco Inc., 2024

# Full characterisation of a MOS (Part 1)

# Required Modules
#  Victory Process
#   -2D Diffusion & Implantation
#   -2D Oxidation
#  Victory Mesh
#   -2D Meshing
#  Victory Device
#   -2D Simulator

# Example Name
Set name=“Bulk_ex04” 

# Full Characterisation of an MOS (Part 1)
# -Id/Vgs and Threshold Voltage Extraction
# -Family of Id/Vds Curves 
# -Sub-Threshold Slope Extraction

####### PROCESS SIMULATION ####### 
# Makes simple symmetrical MOS structure. Only half simulated to save time.
# Mirrored in the remesh stage
GO victoryprocess simflags="-P 4"

INIT meshdepth=2 orientation=100 from=0.0 to=0.5 depth=0.8 gasheight=2.35 resolution=0.01,0.01 material=silicon dopants="phosphorus" dopingvalues="1e14"

LINE x location=0.0 spacing=0.05
LINE x location=0.2 spacing=0.01 
LINE x location=0.5 spacing=0.01 
#
LINE z location=0.0 spacing=0.01 
LINE z location=0.5 spacing=0.01
LINE z location=0.8 spacing=0.05 

#pwell formation including masking off of the nwell
DIFFUSE time=30 temperature=1000 dryo2 press=1.00 hcl=3

STRIP material=oxide

#P-well Implant
IMPLANT boron dose=8e12 energy=100  

DIFFUSE temperature=950 time=100 weto2 hcl=3

# welldrive starts here
DIFFUSE time=50 temperature=1000 t.final=1200 dryo2 press=0.10 hcl=3
DIFFUSE time=220 temperature=1200 press=1
DIFFUSE time=90 temperature=1200 t.final=800 press=1

STRIP material=oxide

# sacrificial "cleaning" oxide
DIFFUSE time=20 temperature=1000 dryo2 press=1 hcl=3

STRIP material=oxide 

# gate oxide grown
DIFFUSE time=11 temperature=925 dryo2 press=1.00 hcl=3

#vt adjust implant 
IMPLANT boron dose=9.5e11 energy=10  

DEPOSIT max material=polysilicon thick=0.2 right.to=0.35

DIFFUSE time=3 temperature=900 weto2 press=1.0

IMPLANT phosphor dose=3.0e13 energy=20  

DEPOSIT conformal material=oxide thick=0.120

ETCH material=oxide dry thick=0.120

IMPLANT arsenic dose=5.0e15 energy=50  

DIFFUSE time=1 temperature=900 press=1.0

# pattern s/d contact metal
ETCH material=oxide left.to=0.2

DEPOSIT left.to=0.18 material=aluminum thick=0.03 min

# Save simulation status for mirror and remesh
SAVE name=$'name'_VP_sv

####### STRUCTURE REMESHING ####### 
GO victorymesh simflags="-P 4"

LOAD in="$'name'_VP_sv"

# mirror about the x axis
MIRROR axes="+1x"

# build the conformal mesh on the mirrored structure
REMESH conformal 

SAVE out="$'name'_VM"

GO victoryextract
LOAD in="$'name'_VM.str"
# Extract a design parameter - gate oxide thickness 
EXTRACT.THICKNESS name="gateox" region="SiO2" nth=1 x.val=0.05 
# extract final S/D Xj
EXTRACT.DEPTH name=nxj x.val=0.1 field=net value=0 region=silicon
# extract the N++ regions sheet resistance
EXTRACT.SHEET.RESISTANCE name="n++ sheet rho" x.val=0.05
# extract the sheet rho under the spacer, of the LDD region
EXTRACT.SHEET.RESISTANCE name="ldd sheet rho" x.val=0.30
# extract the surface conc under the channel.
EXTRACT.SURFACE.CONCENTRATION name="chan surf conc" region=silicon other.region=oxide x.val=0.45 field="Net Doping"


####### DEVICE SIMULATION ####### 
# Vth
GO victoryd simflags="-P 2"

#### STRUCTURE SPECIFICATION ####
# as electrodes were mirrored and not labelled previously
# use no.contact to specify labels will be assigned in Victory Device
MESH inf=$'name'_VM.str no.contact

# Specify co-ordinates in teh electrode reions and electrode names
ELECTRODE coord.region="0.1,0.2" name=source
ELECTRODE coord.region="0.6,0.2" name=gate
ELECTRODE coord.region="1.0,0.2" name=drain
ELECTRODE substrate

#### MATERIAL/PHYSICAL MODELS ####  
MODEL cvt srh print 

CONTACT name=gate n.poly

INTERFACE qf=3e10

#### SOLUTION SOLVING #### 
SOLVE init

SOLVE vstep=0.01 vfinal=0.1 name=drain

# start logging IV data on the electrodes to file
LOG outfile=$'name'_VD_Vth.log

# Ramp the gate
SOLVE vgate=0 vstep=0.1 vfinal=3.0 name=gate

# Save a structure
SAVE outfile=$'name'_VD_Vth.str

# extract device parameters
extract name="nvt" (xintercept(maxslope(curve(abs(v."gate"),abs(i."drain")))) \
	- abs(ave(v."drain"))/2.0)

# Family of IdVd curves
GO victoryd simflags="-P 2"

MESH inf=$'name'_VM.str no.contact

ELECTRODE coord.region="0.1,0.2" name=source
ELECTRODE coord.region="0.6,0.2" name=gate
ELECTRODE coord.region="1.0,0.2" name=drain
ELECTRODE substrate

MODEL cvt srh print 

CONTACT name=gate n.poly

INTERFACE qf=3e10

#### SOLUTION SOLVING #### 

SOLVE init
# ramp up gate voltage and save at certain points to re-load for 
# Vd sweep
SOLVE vstep=0.1 vfinal=1.1 name=gate
SAVE outf=$'name'_VD_tmp1.str
SOLVE vstep=0.1 vfinal=2.2 name=gate
SAVE outf=$'name'_VD_tmp2.str
SOLVE vstep=0.1 vfinal=3.3 name=gate
SAVE outf=$'name'_VD_tmp3.str

#load in temporary files and ramp Vds
LOAD infile=$'name'_VD_tmp1.str
LOG outf=$'name'_VD_1p1.log
SOLVE name=drain vdrain=0 vfinal=3.3 vstep=0.1
LOG off

LOAD infile=$'name'_VD_tmp2.str
LOG outf=$'name'_VD_2p2.log
SOLVE name=drain vdrain=0 vfinal=3.3 vstep=0.1
LOG off

LOAD infile=$'name'_VD_tmp3.str
LOG outf=$'name'_VD_3p3.log
SOLVE name=drain vdrain=0 vfinal=3.3 vstep=0.1
LOG off

# extract max current and saturation slope  
extract name="nidsmax" max(i."drain")
extract name="sat_slope" slope(minslope(curve(v."drain",i."drain")))


# Sub-threshold slope
GO victoryd simflags="-P 2"

MESH inf=$'name'_VM.str no.contact

ELECTRODE coord.region="0.1,0.2" name=source
ELECTRODE coord.region="0.6,0.2" name=gate
ELECTRODE coord.region="1.0,0.2" name=drain
ELECTRODE substrate

MODEL cvt srh print 

CONTACT name=gate n.poly

INTERFACE qf=3e10

#### SOLUTION SOLVING #### 
SOLVE init

# small bias on the drain
SOLVE vstep=0.01 vfinal=0.1 name=drain

# log electrode IV data
LOG outf=$'name'_VD_SubVth.log

# Ramp the gate to a volt
SOLVE vgate=0 vstep=0.1 vfinal=1.0 name=gate

EXTRACT init inf="$'name'_VD_SubVth.log"
EXTRACT name="nsubvt" 1.0/slope(maxslope(curve(abs(v."gate"),log10(abs(i."drain")))))

victoryvisual $'name'_VM.str:$'name'_MeshDop.set \
 $'name'_VD_Vth.log \
 $'name'_VD_1p1.log+$'name'_VD_2p2.log+$'name'_VD_3p3.log \
 $'name'_VD_SubVth.log:$'name'_SubVth.set

QUIT
