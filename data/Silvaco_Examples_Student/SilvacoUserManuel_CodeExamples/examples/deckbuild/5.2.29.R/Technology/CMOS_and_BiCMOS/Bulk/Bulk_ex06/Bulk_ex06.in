# (c) Silvaco Inc., 2024

# Full characterisation of a MOS (Part 3)

# Required Modules
#  Victory Process
#   -2D Diffusion & Implantation
#   -2D Oxidation
#  Victory Mesh
#   -2D Meshing
#  Victory Device
#   -2D Simulator

# Example Name
Set name=“Bulk_ex06” 


# Full Characterisation of an MOS (Part 3)
# -Import 1D doping curves into Victory Process
# -Transient simulation of gate turn-on
# -Drain/Gate Overlap Capacitance

####### PROCESS SIMULATION ####### 
# First part does a 1D implantation simulation then extracts
# Doping profile to be used in full device process simulation
GO victoryprocess simflags="-P 4"

INIT meshdepth=2 orientation=100 from=0.0 to=0.1 depth=0.8 gasheight=2.35 resolution=0.01,0.01 material=silicon dopants="phosphorus" dopingvalues="1e14"

LINE x location=0.0 spacing=0.05
LINE x location=0.1 spacing=0.05 
#
LINE z location=0.0 spacing=0.01 
LINE z location=0.5 spacing=0.01
LINE z location=0.8 spacing=0.05 

IMPLANT arsenic dose=5.0e15 energy=50 

EXPORT name=$'name'_VP.str

# Extract profle to be used in subsequent process simulation
go victoryextract
LOAD in=$'name'_VP.str
OPTION STRIP.HEADER 
OPTION DELIM=" "
EXTRACT.PROFILE csvfile="$'name'_Prof" regions="silicon" field="Arsenic" 

GO victoryprocess simflags="-P 4"

INIT meshdepth=2 orientation=100 from=0.0 to=0.5 depth=0.8 gasheight=2.35 resolution=0.01,0.01 material=silicon dopants="phosphorus" dopingvalues="1e14"

LINE x location=0.0 spacing=0.05
LINE x location=0.2 spacing=0.01 
LINE x location=0.5 spacing=0.01 
#
LINE z location=0.0 spacing=0.01 
LINE z location=0.5 spacing=0.01
LINE z location=0.8 spacing=0.05 

#pwell formation including masking off of the nwell
DIFFUSE time=30 temperature=1000 dryo2 press=1.00 hcl=3

STRIP material=oxide

#P-well Implant
IMPLANT boron dose=8e12 energy=100 

DIFFUSE temperature=950 time=100 weto2 hcl=3

# welldrive starts here
DIFFUSE time=50 temperature=1000 t.final=1200 dryo2 press=0.10 hcl=3
DIFFUSE time=220 temperature=1200 press=1
DIFFUSE time=90 temperature=1200 t.final=800 press=1

STRIP material=oxide

# sacrificial "cleaning" oxide
DIFFUSE time=20 temperature=1000 dryo2 press=1 hcl=3

STRIP material=oxide 

# gate oxide grown
DIFFUSE time=11 temperature=925 dryo2 press=1.00 hcl=3

#vt adjust implant 
IMPLANT boron dose=9.5e11 energy=10 

DEPOSIT max material=polysilicon thick=0.2 right.to=0.35

DIFFUSE time=3 temperature=900 weto2 press=1.0

IMPLANT phosphor dose=3.0e13 energy=20 

DEPOSIT conformal material=oxide thick=0.120

ETCH material=oxide dry thick=0.120

# Restrict imported profile to a certain area
MASK right.to=0.2225

# Load in 1D doping profile and set lateral diffusion to match
# 2D implant edges
PROFILE infile=$'name'_Prof.dat arsenic lateral.std.dev=0.016

# Strip last deposited layer "barrier" - from mask statement
STRIP 

DIFFUSE time=1 temperature=900 press=1.0

# pattern s/d contact metal
ETCH material=oxide left.to=0.2

DEPOSIT left.to=0.18 material=aluminum thick=0.03 min

# Save simulation status for mirror and remesh
SAVE name=$'name'_VP_sv

####### STRUCTURE REMESHING ####### 
GO victorymesh simflags="-P 4"

LOAD in="$'name'_VP_sv"

# mirror about the x axis
MIRROR axes="+1x"

# build the conformal mesh on the mirrored structure
REMESH conformal 

SAVE out="$'name'_VM"

GO victoryextract
LOAD in="$'name'_VM.str"
# Extract a design parameter - gate oxide thickness 
EXTRACT.THICKNESS name="gateox" region="SiO2" nth=1 x.val=0.05 
# extract final S/D Xj
EXTRACT.DEPTH name=nxj x.val=0.1 field=net value=0 region=silicon
# extract the N++ regions sheet resistance
EXTRACT.SHEET.RESISTANCE name="n++ sheet rho" x.val=0.05
# extract the sheet rho under the spacer, of the LDD region
EXTRACT.SHEET.RESISTANCE name="ldd sheet rho" x.val=0.30
# extract the surface conc under the channel.
EXTRACT.SURFACE.CONCENTRATION name="chan surf conc" region=silicon other.region=oxide x.val=0.45 field="Net Doping"


####### DEVICE SIMULATION ####### 
# Transient simulation of gate turn-on
GO victoryd simflags="-P 2"

#### STRUCTURE SPECIFICATION ####
# as electrodes were mirrored and not labelled previously
# use no.contact to specify electrode labels will be 
# assigned in Victory Device
MESH inf=$'name'_VM.str no.contact

# Specify co-ordinates in the electrode regions and electrode names
ELECTRODE coord.region="0.1,0.2" name=source
ELECTRODE coord.region="0.6,0.2" name=gate
ELECTRODE coord.region="1.0,0.2" name=drain
ELECTRODE substrate

#### MATERIAL/PHYSICAL MODELS ####  
MODELS bgn cvt consrh auger print 

INTERFACE qf=3e10

CONTACT name=gate n.poly

# Set drain capacitance to 0.5pF/um, the will mimik a Pchan in an inverter...
CONTACT name=drain cap=0.5e-12 resis=1.0e5

#### NUMERICAL METHODS ####
# limit maximum timestep   
METHOD dt.max=2E-10

#### SOLUTION SOLVING #### 
SOLVE init
SOLVE prev

# Calculate solution at VDS = 5.0V
SOLVE vdrain=0 vstep=0.1 vfinal=5.0 name=drain

# Start logging data on electrodes
LOG outfile=$'name'_Tran.log 

# Apply Gate turn-on transient and save structures at 
# certain time points. Time values appended to file names
SOLVE vgate=5.0 ramptime=5e-09 dt=2e-11 tstop=30e-09 \
 outfile=$'name'_T.str tsave="3e-9,4.5e-9,6e-9" tsave.name

GO victoryextract
LOAD in=$'name'_Tran.log 
CURVE name=idvd x="Drain Voltage" y="Drain Current"
EXTRACT.CURVE name="Surge Current" expression="maxyy(curve.idvd)"

####### DEVICE SIMULATION ####### 
# -Drain/Gate Overlap Capacitance
GO victoryd simflags="-P 2"

#### STRUCTURE SPECIFICATION ####
# as electrodes were mirrored and not labelled previously
# use no.contact to specify electrode labels will be 
# assigned in Victory Device
MESH inf=$'name'_VM.str no.contact

# Specify co-ordinates in the electrode regions and electrode names
ELECTRODE coord.region="0.1,0.2" name=source
ELECTRODE coord.region="0.6,0.2" name=gate
ELECTRODE coord.region="1.0,0.2" name=drain
ELECTRODE substrate

#### MATERIAL/PHYSICAL MODELS ####  
MODELS bgn cvt consrh auger print 

INTERFACE qf=3e10

CONTACT name=gate n.poly

#### SOLUTION SOLVING #### 
SOLVE init
SOLVE prev

# ramp VGS to get GATE and OVERLAP CAPACITANCE
SOLVE vgate=0 vstep=-0.1 vfinal=-2 name=gate

# Start logging data on electrodes
LOG outfile=$'name'_CV.log 

# CV on sweep of gate voltage
SOLVE vstep=0.2 vfinal=2 name=gate ac freq=1e6 aname=gate

victoryvisual $'name'_Tran.log:$'name'_Tran.set \
 $'name'_CV.log:$'name'_CV.set

quit
