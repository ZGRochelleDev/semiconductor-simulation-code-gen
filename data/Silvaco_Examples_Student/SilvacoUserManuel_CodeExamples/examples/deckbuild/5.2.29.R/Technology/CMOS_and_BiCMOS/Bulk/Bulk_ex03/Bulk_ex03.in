# (c) Silvaco Inc., 2024

# Lattice heating in a cryo-MOSFET  

# Required Modules
#  Victory Device
#   -2D Simulator
#   -2D Giga

# Example Name
Set name=“Bulk_ex03” 

####### DEVICE SIMULATION #######
GO victorydevice simflags="-P 4 -160"

#### STRUCTURE SPECIFICATION ####
# Mesh Definition
MESH

X.MESH location=0.0 spacing=0.02
X.MESH location=0.7 spacing=0.004
X.MESH location=1.0 spacing=0.02

Y.MESH location=0.0 spacing=0.001
Y.MESH location=0.01 spacing=0.001
Y.MESH location=0.02 spacing=0.001
Y.MESH location=0.1 spacing=0.01
Y.MESH location=1.0 spacing=0.1

# Regions and electrodes
REGION num=1 oxide y.max=0.01
REGION num=2 silicon y.min=0.01

ELECTRODE name=source y.min=0.01 y.max=0.012 x.max=0.2
ELECTRODE name=drain y.min=0.01 y.max=0.012 x.min=0.8
ELECTRODE name=gate y.max=0.0 x.min=0.3 x.max=0.7
ELECTRODE name=substrate bottom

# Device doping
DOPING gaussian char=0.1 x.max=0.2 conc=1.0e19 n.type
DOPING gaussian char=0.1 x.min=0.8 conc=1.0e19 n.type
DOPING uniform p.type conc=1.0e14

#### MATERIAL/PHYSICAL MODELS #### 
INTERFACE qf=-1e10

CONTACT name=gate workf=4.5

# Choose ALMA thermal conductivity for low temperature.
MATERIAL material=silicon lt.taun=-1.5 lt.taup=-1.5 tcon.alma 
MATERIAL material=polysi tcon.alma

THERMCONTACT num=1 elec.num=1 ext.temp=10.0
THERMCONTACT num=2 elec.num=2 ext.temp=10.0

MODELS srh analytic print temp=10 lat.temp heat.full 

MOBILITY alphan.caug=0.0 betan.caug=0.0 gamman.caug=0.0 alphap.caug=0.0 betap.caug=0.0 gammap.caug=0.0

METHOD newton min.temp=3.0 max.temp=400.0 dvmax=0.05 itlimit=200 gumits=250 direct clim.lat=1.0e8 climit=1.0e-10

#### SOLUTION SOLVING #### 
SOLVE init
SOLVE prev

OUTPUT con.band val.band

SOLVE vgate=3.0 nocurrent

SOLVE prev

SOLVE vdrain=1.0e-6
SOLVE vdrain=1.0e-5
SOLVE vdrain=1.0e-4
SOLVE vdrain=1.0e-3

LOG outf=$'name'_VD.log
SOLVE vdrain=0.05 vstep=0.05 vfinal=3.0 name=drain prev
LOG off

SAVE outf=$'name'_VD.str

victoryvisual $'name'_VD.log \
 $'name'_VD.log:$'name'_VD_log.set \
 $'name'_VD.str:$'name'_VD_str.set 

QUIT
