* Wien-bridge Oscillator - HOSCIL Analysis

.GLOBAL vcc vee gnd

.option nomod ;brief=1
.param Rref=1000

.include Wien-br_Amplifier.inc
.include Wien-br_FeedbackNet.inc

; Sources
Vcc  vcc  gnd  dc=15
Vee  vee  gnd  dc=-15

; Op Amp
XAmp1  1    2    3    ua741 

; Resistors
R1      2   gnd     r=10k

; Amplitude stabilization network
R3      2    3      r=21k
R4      2    4      r=200k
D1      4    3      DioN
D2      3    4      DioN

; Tunable capacitance
.param Ctune=1nF

; RC feedback network
XRes1   3   1   gnd   FeedbackNet 

.model DioN   D  is=.1fA

************
* Analysis *
************
; Oscillation Frequency estimation
.param fund_osc_init = 10.073kHz  ;theoretical value
*.param fund_osc_init = 9.4733kHz   ;approximated result
* .param fund_osc_init = 12.591kHz    ;for parametric sweep

.tran 10n 50ms
.probe v(1) v(2) v(3) v(4)
.measure Clock jitter v(3) from=10m to=50m fall='1' val=0.0 targ=v(3) fall='370' val='0.0'
*.measure Clock jitter v(3) from=10m to=50m fall='1' val=0.0 targ=v(3) fall='188' val='0.0'

#COM
.HOSCIL probe(3,1)   
* + v(3)
* + DEC 10 -500Hz 500Hz
* + Ftype=REL Relharm=1 Faxis=REL
+ fund_osc=fund_osc_init nharm=3
+ newton_accuracy=high
+ fund_osc_reltol=0.1
+ annotate=1
+ waves=2000
* + sweep Ctune 0.8n 1.2nF 0.05nF
***********
* Results *
***********
.PROBE hop v(3) v(4)
.LET HOP_SP 
+ Power_dBm='(30.0+10.0*log10(0.5*mag(v(3))*mag(v(3))/Rref))' 
#ENDCOM

* Extraction of oscillation frequency
* .measure hop_sp fosc AMAX vdb(3)

.end
